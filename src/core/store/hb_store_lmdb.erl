%% @doc An LMDB (Lightning Memory Database) implementation of the HyperBeam store interface.
%%
%% This module provides a persistent key-value store backend using LMDB, which is a
%% high-performance embedded transactional database. The implementation follows a
%% singleton pattern where each database environment gets its own dedicated server
%% process to manage transactions and coordinate writes.
%%
%% Key features include:
%% <ul>
%%   <li>Asynchronous writes with batched transactions for performance</li>
%%   <li>Automatic link resolution for creating symbolic references between keys</li>
%%   <li>Group support for organizing hierarchical data structures</li>
%%   <li>Prefix-based key listing for directory-like navigation</li>
%%   <li>Process-local caching of database handles for efficiency</li>
%% </ul>
%%
%% The module implements a dual-flush strategy: writes are accumulated in memory
%% and flushed either after an idle timeout or when explicitly requested during
%% read operations that encounter cache misses.
-module(hb_store_lmdb).

%% Public API exports
-export([start/3, stop/3, scope/0, scope/1, reset/3]).
-export([read/3, write/3, list/3, match/3]).
-export([group/3, link/3, type/3, resolve/3]).
-export([sync/3, delete/3]).

%% Test framework and project includes
-include_lib("eunit/include/eunit.hrl").
-include("include/hb.hrl").

%% Configuration constants with reasonable defaults
-define(DEFAULT_SIZE, 2 * 1024 * 1024 * 1024 * 1024). % 2TiB default database size
-define(DEFAULT_BATCH_SIZE, 5_000).             % Flush keys on every read or 
                                                % every 5,000 write operations.

%% @doc Start the LMDB storage system for a given database configuration.
%%
%% This function initializes or connects to an existing LMDB database instance.
%% It uses a singleton pattern, so multiple calls with the same configuration
%% will return the same server process. The server process manages the LMDB
%% environment and coordinates all database operations.
%%
%% The StoreOpts map must contain a "prefix" key specifying the
%% database directory path. Also the required configuration includes "capacity"
%% for the maximum database size and flush timing parameters.
%%
%% @param StoreOpts A map containing database configuration options
%% @returns {ok, ServerPid} on success, {error, Reason} on failure
start(Opts = #{ <<"name">> := DataDir }, _Req, _NodeOpts) ->
    init_prometheus(),
    % Ensure the directory exists before opening LMDB environment
    DataDirPath = hb_util:list(DataDir),
    ok = ensure_dir(DataDirPath),
    EnvOpts =
        [
            {
                map_size,
                hb_util:int(maps:get(<<"capacity">>, Opts, ?DEFAULT_SIZE))
            },
            {
                batch_size,
                hb_util:int(maps:get(<<"batch-size">>, Opts, ?DEFAULT_BATCH_SIZE))
            },
            no_mem_init,
            no_sync
        ] ++
        case maps:get(<<"read-ahead">>, Opts, true) of
            true -> [];
            false -> [no_readahead]
        end ++
        case maps:get(<<"read-only">>, Opts, false) of
            true -> [no_lock];
            false -> []
        end ++
        case maps:get(<<"max-readers">>, Opts, false) of
            false -> [];
            MaxReaders -> [{max_readers, hb_util:int(MaxReaders)}]
        end ++
        case maps:get(<<"lock">>, Opts, true) of
            true -> [];
            false -> [no_lock]
        end,
    % Create the LMDB environment with specified size limit
    {ok, Env} = elmdb:env_open(DataDirPath, EnvOpts),
    {ok, DBInstance} = elmdb:db_open(Env, [create]),
    {ok, #{ <<"env">> => Env, <<"db">> => DBInstance }};
start(_Store, _Req, _NodeOpts) ->
    {error, {badarg, <<"StoreOpts must be a map">>}}.

%% @doc Ensure that the database directory exists.
ensure_dir(DataDirPath) ->
    % `filelib` interprets the last path element as a filename, so we add a 
    % dummy one, else the final directory will not be created.
    filelib:ensure_dir(filename:join(DataDirPath, "dummy.mdb")).

%% @doc Determine whether a key represents a simple value or composite group.
%%
%% This function reads the value associated with a key and examines its content
%% to classify the entry type. Keys storing the literal binary "group" are
%% considered composite (directory-like) entries, while all other values are
%% treated as simple key-value pairs.
%%
%% This classification is used by higher-level HyperBeam components to understand
%% the structure of stored data and provide appropriate navigation interfaces.
%%
%% @param Opts Database configuration map
%% @param KeyReq Request of the form `#{<<"type">> => Key}`.
%% @returns `{ok, composite}` for group entries, `{ok, simple}` for regular
%%          values, or `{error, not_found}`.
type(Opts, #{ <<"type">> := Key }, _NodeOpts) ->
    KeyBin =
        case is_binary(Key) of
            true -> Key;
            false -> hb_path:to_binary(Key)
        end,
    case read_resolved(Opts, KeyBin) of
        {ok, _ResolvedKey, <<"group">>} -> {ok, composite};
        {ok, _ResolvedKey, _Value} -> {ok, simple};
        not_found -> {error, not_found}
    end.

%% @doc Write a key-value pair to the database asynchronously.
%%
%% Request maps are folded into individual writes and each entry is sent to the
%% database server process immediately without waiting for the write to be
%% committed to disk. The server accumulates writes in a transaction that is
%% periodically flushed based on timing constraints or explicit flush requests.
%%
%% The asynchronous nature provides better performance for write-heavy workloads
%% while the batching strategy ensures data consistency and reduces I/O overhead.
%% However, recent writes may not be immediately visible to readers until the
%% next flush occurs.
%%
%% @param Opts Database configuration map
%% @param Req Either a request map of `Path => Value` pairs or an internal
%%            `Path, Value` pair used while folding that map.
%% @returns `ok` immediately on success, or an error tuple on failure
write(#{ <<"read-only">> := true }, _Req, _NodeOpts) when is_map(_Req) ->
    {error, not_found};
write(Opts, Req, _NodeOpts) when is_map(Req) ->
    maps:fold(
        fun(Path, Value, ok) when is_binary(Path) ->
            write(Opts, Path, Value);
           (Path, Value, ok) ->
            write(Opts, hb_path:to_binary(Path), Value);
           (_Path, _Value, Error) ->
            Error
        end,
        ok,
        Req
    );
write(#{ <<"read-only">> := true }, _PathParts, _Value) ->
    {error, not_found};
write(Opts, PathParts, Value) when is_list(PathParts) ->
    write(Opts, hb_store_utils:to_path(PathParts), Value);
write(Opts, Path, Value) ->
    #{ <<"db">> := DBInstance } = find_env(Opts),
    ?event_debug({elmdb_write, {db, DBInstance}, {path, Path}, {value, Value}}),
    case elmdb:put(DBInstance, Path, Value) of
        ok -> ok;
        {error, Type, Description} ->
            ?event(
                error,
                {lmdb_error,
                    {type, Type},
                    {description, Description}
                }
            ),
            retry
    end.

%% @doc Delete exact keys: `#{ <<"delete">> => [Key] }'.
%%
%% No link is followed and no subtree is implied -- the caller names every row.
%% The elmdb write worker commits the overlay and deletes in one transaction,
%% so a write that returned before this call cannot be resurrected by a later
%% flush, and a read after this call returns `not_found' until the key is
%% written again. Absent keys are skipped. LMDB returns the freed pages to its
%% free list for reuse; the file does not shrink.
%%
%% With `<<"guarded">> => true' the delete is vetoed as a whole, atomically
%% with the commit, if any key was written -- or named by a written value --
%% since elmdb write tracking was switched on or last taken
%% (`elmdb:track/2', `elmdb:track_take/1'); the result is then
%% `{error, {conflict, Keys}}' and nothing is deleted.
%% @returns `{ok, Deleted}', the number of keys that existed.
delete(#{ <<"read-only">> := true }, _Req, _NodeOpts) ->
    {error, read_only};
delete(Opts, Req = #{ <<"delete">> := Keys }, _NodeOpts) when is_list(Keys) ->
    #{ <<"db">> := DB } = find_env(Opts),
    Bins = [ hb_util:bin(K) || K <- Keys ],
    Result =
        case maps:get(<<"guarded">>, Req, false) of
            true -> elmdb:delete_batch_guarded(DB, Bins);
            _ -> elmdb:delete_batch(DB, Bins)
        end,
    case Result of
        {ok, N} -> {ok, N};
        {error, conflict, Found} -> {error, {conflict, Found}};
        {error, Type, Description} ->
            ?event(error, {lmdb_delete_failed, Type, Description}),
            {error, {Type, Description}}
    end.

%% @doc Make every write that has already returned durable.
%%
%% `write/3' only places a value in elmdb's in-memory overlay, which is committed
%% to LMDB when the overlay reaches `batch-size' or a listing flushes it, so a
%% crash of the node loses everything written since the last commit. At level
%% `commit' (the default) this function commits the overlay, after which the
%% writes are in the OS page cache and survive the node process being killed.
%% At level `fsync' it also syncs the environment to disk (the store is opened
%% `no_sync'), so the writes survive a host crash or power loss.
%%
%% Callers are grouped: one process per environment performs the commits, and
%% every request that arrives while a commit is in progress is served by the
%% next single commit. Concurrent callers therefore share the cost of each
%% commit rather than queueing one commit each.
sync(#{ <<"read-only">> := true }, _Req, _NodeOpts) ->
    ok;
sync(Opts = #{ <<"name">> := Name }, Req, _NodeOpts) ->
    Level =
        case maps:get(<<"level">>, Req, commit) of
            <<"fsync">> -> fsync;
            fsync -> fsync;
            _ -> commit
        end,
    #{ <<"db">> := DB, <<"env">> := Env } = ensure_env(Opts),
    Syncer = hb_name:singleton({?MODULE, syncer, Name}, fun syncer/0),
    Ref = erlang:monitor(process, Syncer),
    Syncer ! {sync, self(), Ref, Level, DB, Env},
    receive
        {synced, Ref, Result} ->
            erlang:demonitor(Ref, [flush]),
            Result;
        {'DOWN', Ref, process, Syncer, Reason} ->
            {error, {lmdb_syncer_down, Reason}}
    end.

%% @doc The group-commit loop for one environment. It waits for a request, then
%% gathers every request already queued behind it, serves them all with a single
%% commit (and, if any asked for it, a single environment sync), and replies.
syncer() ->
    receive
        {sync, From, Ref, Level, DB, Env} ->
            syncer_batch([{From, Ref}], Level, DB, Env)
    end.

syncer_batch(Waiters, Level, DB, Env) ->
    receive
        {sync, From, Ref, NextLevel, NextDB, NextEnv} ->
            syncer_batch(
                [{From, Ref} | Waiters],
                case NextLevel of
                    fsync -> fsync;
                    _ -> Level
                end,
                NextDB,
                NextEnv
            )
    after 0 ->
        Result = sync_now(Level, DB, Env),
        lists:foreach(
            fun({From, Ref}) -> From ! {synced, Ref, Result} end,
            Waiters
        ),
        syncer()
    end.

%% @doc Commit the overlay and, at level `fsync', sync the environment.
sync_now(Level, DB, Env) ->
    try elmdb:flush(DB) of
        ok when Level == fsync ->
            case elmdb:env_sync(Env) of
                ok -> ok;
                {error, Type, Description} ->
                    ?event(error, {lmdb_sync_failed, Type, Description}),
                    {error, {Type, Description}}
            end;
        ok ->
            ok;
        {error, Type, Description} ->
            ?event(error, {lmdb_commit_failed, Type, Description}),
            {error, {Type, Description}}
    catch
        Class:Reason ->
            ?event(error, {lmdb_sync_failed, Class, Reason}),
            {error, {Class, Reason}}
    end.

%% @doc Read a value from the database by key, with automatic link resolution.
%%
%% This function attempts to read a value directly from the committed database.
%% If the key is not found, it resolves links in the path and retries the read.
%%
%% The function automatically handles link resolution: if a stored value begins
%% with the "link:" prefix, it extracts the target key and recursively reads
%% from that location instead. This creates a symbolic link mechanism that
%% allows multiple keys to reference the same underlying data.
%%
%% Link resolution is transparent to the caller and can chain through multiple
%% levels of indirection, though care should be taken to avoid circular
%% references.
%%
%% @param Opts Database configuration map
%% @param PathReq Request of the form `#{<<"read">> => Path}`.
%% @returns `{ok, Value}` on success, `{composite, Keys}` for groups, or
%%          `{error, not_found}` on failure
read(Opts, #{ <<"read">> := Path }, _NodeOpts) when is_binary(Path) ->
    read_result(Opts, Path);
read(Opts, #{ <<"read">> := Path }, _NodeOpts) ->
    read_result(Opts, hb_path:to_binary(Path)).

%% A single `read_prefix' over the bare `Path' (no trailing slash) returns the
%% marker row (key == `Path') alongside every descendant in one cursor scan, so
%% the marker that classifies the entry — value, link, or group — arrives in the
%% same seek as the children. Only a genuine miss (the key is absent and must be
%% reached through an intermediate link) falls through to the resolver.
read_result(Opts, Path) ->
    EnvOpts = ensure_env(Opts),
    StartTime = erlang:monotonic_time(),
    Result = 
        case read_prefix_rows(EnvOpts, Path) of
            {ok, Rows} ->
                case prefix_read_result(EnvOpts, Path, Rows) of
                    {error, not_found} -> read_prefix_miss(EnvOpts, Path);
                    R -> R
                end;
            not_found ->
                read_prefix_miss(EnvOpts, Path);
            {error, _} = Error ->
                Error;
            {error, Type, _} ->
                {error, Type}
        end,
    MetricStatus = 
        case Result of
            {ok, _} -> hit;
            {composite, _} -> hit;
            {error, not_found} -> miss;
            not_found -> miss;
            _ -> unknown
        end,
    sample_metrics(EnvOpts, StartTime, MetricStatus),
    Result.

%% The literal `Path' was not present in the scan. Resolve any intermediate
%% links in the path and, if that yields a different key, retry the read against
%% the resolved target. Content-addressed `data' keys never carry links, so they
%% short-circuit straight to `not_found' without a resolver walk.
read_prefix_miss(Opts, Path) ->
    case hb_store_utils:is_data_path(Path) of
        true ->
            {error, not_found};
        false ->
            try
                PathParts = binary:split(Path, <<"/">>, [global, trim_all]),
                Read = fun(Key) -> read_direct(Opts, Key) end,
                case hb_store_utils:resolve_path_links(Read, PathParts) of
                    {ok, ResolvedPathParts} ->
                        case hb_store_utils:to_path(ResolvedPathParts) of
                            Path -> {error, not_found};
                            ResolvedPath -> read_result(Opts, ResolvedPath)
                        end;
                    {error, _} ->
                        {error, not_found}
                end
            catch
                Class:Reason:Stacktrace ->
                    ?event(error,
                        {
                            resolve_path_links_failed,
                            {class, Class},
                            {reason, Reason},
                            {stacktrace, {trace, Stacktrace}},
                            {path, Path}
                        }
                    ),
                    {error, not_found}
            end
    end.

read_resolved(#{<<"name">> := Name} = Opts, Path) ->
    EnvOpts = ensure_env(Opts),
    PathBin =
        case is_binary(Path) of
            true -> Path;
            false -> hb_path:to_binary(Path)
        end,
    StartTime = erlang:monotonic_time(),
    case do_read_resolved(EnvOpts, PathBin) of
        {ok, _ResolvedPath, _Value} = Result ->
            sample_metrics(Name, StartTime, hit),
            Result;
        not_found ->
            sample_metrics(Name, StartTime, miss),
            not_found
    end.

do_read_resolved(Opts, Path) ->
    case read_with_links(Opts, Path) of
        {ok, _ResolvedPath, _Value} = Result ->
            Result;
        not_found ->
            case Path of
                <<"data">> ->
                    not_found;
                <<"data/", _/binary>> ->
                    not_found;
                _ ->
                    try
                        PathParts = binary:split(Path, <<"/">>, [global, trim_all]),
                        Read = fun(Key) -> read_direct(Opts, Key) end,
                        Resolved =
                            hb_store_utils:resolve_path_links(Read, PathParts),
                        case Resolved of
                            {ok, ResolvedPathParts} ->
                                Target =
                                    hb_store_utils:to_path(ResolvedPathParts),
                                read_with_links(Opts, Target);
                            {error, _} ->
                                not_found
                        end
                    catch
                        Class:Reason:Stacktrace ->
                            ?event(error,
                                {
                                    resolve_path_links_failed,
                                    {class, Class},
                                    {reason, Reason},
                                    {stacktrace, {trace, Stacktrace}},
                                    {path, Path}
                                }
                            ),
                            not_found
                    end
            end
    end.

%% @doc Unified read function that handles LMDB reads with fallback to the
%% in-process pending writes, if necessary.
%%
%% Returns `{ok, Value}` or `not_found`.
read_direct(#{<<"db">> := DBInstance, <<"name">> := Name}, Path) ->
    read_direct(DBInstance, Name, Path);
read_direct(#{<<"db">> := DBInstance}, Path) ->
    read_direct(DBInstance, undefined, Path);
read_direct(#{<<"name">> := Name} = Opts, Path) ->
    #{ <<"db">> := DBInstance } = find_env(Opts),
    read_direct(DBInstance, Name, Path).

read_direct(DBInstance, Name, Path) ->
    case elmdb:get(DBInstance, Path) of
        {ok, Value} -> {ok, Value};
        {error, not_found} -> not_found;
        not_found -> not_found;
        {error, transaction_error, Message} = Err -> 
            ?event(lmdb_store, 
                {transaction_error, 
                    {path, Path}, 
                    {db_name, Name},
                    {message, Message}}),
            Err;
        {error, database_error, ErrorMessage} = Err ->
            ?event(lmdb_store, 
                {database_error, 
                    {path, Path}, 
                    {db_name, Name},
                    {msg, ErrorMessage}}),
            Err
    end.

%% @doc Read a value directly from the database with link resolution.
%% This is the internal implementation that handles actual database reads.
read_with_links(Opts, Path) ->
    case read_direct(Opts, Path) of
        {ok, Value} ->
            case hb_store_utils:is_link(Value) of
                {true, Link} -> 
                    do_read_resolved(Opts, Link);
                false ->
                    {ok, Path, Value}
            end;
        not_found ->
            not_found
    end.

%% @doc Return the scope of this storage backend.
%%
%% The LMDB implementation is always local-only and does not support distributed
%% operations. This function exists to satisfy the HyperBeam store interface
%% contract and inform the system about the storage backend's capabilities.
%%
%% @returns 'local' always
-spec scope() -> local.
scope() -> local.

%% @doc Return the scope of this storage backend (ignores parameters).
%%
%% This is an alternate form of scope/0 that ignores any parameters passed to it.
%% The LMDB backend is always local regardless of configuration.
%%
%% @param _Opts Ignored parameter
%% @returns 'local' always  
-spec scope(term()) -> local.
scope(_) -> scope().

%% @doc List all keys that start with a given prefix.
%%
%% This function provides directory-like navigation by finding all keys that
%% begin with the specified path prefix. It uses the native elmdb:list/2 function
%% to efficiently scan through the database and collect matching keys.
%%
%% The implementation returns only the immediate children of the given path,
%% not the full paths. For example, listing "colors/" will return ["red", "blue"]
%% not ["colors/red", "colors/blue"].
%%
%% If the Path points to a link, the function resolves the link and lists
%% the contents of the target directory instead.
%%
%% This is particularly useful for implementing hierarchical data organization
%% and providing tree-like navigation interfaces in applications.
%%
%% @param StoreOpts Database configuration map
%% @param Path Binary prefix to search for
%% @returns {ok, [Key]} list of matching keys, {error, Reason} on failure
list(Opts, Req = #{ <<"list">> := Path }, _NodeOpts) ->
    EnvOpts = ensure_env(Opts),
    PathBin =
        case is_binary(Path) of
            true -> Path;
            false -> hb_path:to_binary(Path)
        end,
    case read_resolved(EnvOpts, PathBin) of
        {ok, ResolvedPath, <<"group">>} ->
            list_children(EnvOpts, ResolvedPath, Req);
        {ok, _ResolvedPath, _Value} ->
            {error, not_found};
        not_found ->
            {error, not_found}
    end.

%% @doc The children of a group through the NIF's cursor: every one, or
%% those the request names from its `from' in its direction, no more than
%% its limit -- a batch being every child: LMDB reads a page at a time only
%% from a key's fixed-size duplicate values, and the database holds none.
list_children(Opts, ResolvedPath, Req) ->
    #{
        <<"from">> := From,
        <<"limit">> := Limit,
        <<"direction">> := Direction
    } = hb_store_utils:list_request_bounds(Req),
    #{ <<"db">> := DBInstance } = find_env(Opts),
    Options =
        [ {from, From} || From =/= none ] ++
        [ {limit, Limit} || Limit =/= all ] ++
        [ {direction, case Direction of asc -> forward; desc -> backward end} ],
    Prefix = hb_store_utils:child_prefix(ResolvedPath),
    case elmdb:list(DBInstance, Prefix, Options) of
        {ok, Children} -> {ok, Children};
        {error, Type, Description} -> {error, {Type, Description}}
    end.

read_prefix_rows(Opts, Path) ->
    #{ <<"db">> := DBInstance } = find_env(Opts),
    case elmdb:read_prefix(DBInstance, Path) of
        {ok, Rows} -> {ok, Rows};
        {error, not_found} -> not_found;
        not_found -> not_found;
        {error, _Type, _Description} = Error -> Error
    end.

%% Classify the first (marker) row of a bare-prefix scan. `read_prefix' returns
%% keys in lexicographic order, so the row whose key equals `Path' — when it
%% exists — always sorts ahead of the `Path/...' descendants and lands first.
%% A `link:' marker chases its target, a `group' marker becomes a composite of
%% its immediate children, and any other marker is a simple value. When no
%% marker row is present the path is an implicit group: its descendants (if any)
%% still resolve to a composite, otherwise the read is a miss.
prefix_read_result(Opts, Path, [{Path, <<"link:", Link/binary>>} | _])
        when byte_size(Link) > 0 ->
    read_result(Opts, Link);
prefix_read_result(_Opts, Path, [{Path, <<"group">>} | Rows]) ->
    Prefix = hb_store_utils:child_prefix(Path),
    {composite, hb_store_utils:immediate_children(Prefix, Rows)};
prefix_read_result(_Opts, Path, [{Path, Value} | _]) ->
    {ok, Value};
prefix_read_result(_Opts, Path, Rows) ->
    Prefix = hb_store_utils:child_prefix(Path),
    Children = hb_store_utils:immediate_children(Prefix, Rows),
    case {hb_store_utils:is_data_path(Path), Children} of
        {false, [_ | _] = Children} -> {composite, Children};
        _ -> {error, not_found}
    end.

%% @doc Match a series of keys and values against the database. Returns 
%% `{ok, Matches}' if the match is successful, or `not_found' if there are no
%% messages in the store that feature all of the given key-value pairs. `Matches'
%% is given as a list of IDs.
match(Opts, MatchMap, _NodeOpts) when is_map(MatchMap) ->
    match(Opts, maps:to_list(MatchMap), #{});
match(Opts, MatchKVs, _NodeOpts) ->
    #{ <<"db">> := DBInstance } = find_env(Opts),
    Patterns =
        lists:map(
            fun({Key, Value}) -> {Key, hb_util:bin(Value)} end,
            MatchKVs
        ),
    ?event_debug({elmdb_match, MatchKVs}),
    match_patterns(DBInstance, Patterns).

match_patterns(DBInstance, Patterns) ->
    case elmdb:match(DBInstance, Patterns) of
        {ok, Matches} ->
            ?event_debug({elmdb_matched, Matches}),
            {ok, Matches};
        {error, not_found} -> {error, not_found};
        not_found -> {error, not_found}
    end.

%% @doc Create a group entry that can contain other keys hierarchically.
%%
%% Groups in the HyperBeam system represent composite entries that can contain
%% child elements, similar to directories in a filesystem. This function creates
%% a group by storing the special value "group" at the specified key.
%%
%% The group mechanism allows applications to organize data hierarchically and
%% provides semantic meaning that can be used by navigation and visualization
%% tools to present appropriate user interfaces.
%%
%% Groups can be identified later using `type/3', which will return
%% 'composite' for group entries versus 'simple' for regular key-value pairs.
%%
%% @param Opts Database configuration map
%% @param GroupName Binary name for the group
%% @returns Result of the write operation
group(Opts, #{ <<"group">> := GroupName }, _NodeOpts) ->
    case is_binary(GroupName) of
        true -> write(Opts, GroupName, <<"group">>);
        false -> write(Opts, hb_path:to_binary(GroupName), <<"group">>)
    end.

%% @doc Ensure all parent groups exist for a given path.
%%
%% This function creates the necessary parent groups for a path, similar to
%% how filesystem stores use ensure_dir. For example, if the path is
%% "a/b/c/file", it will ensure groups "a", "a/b", and "a/b/c" exist.
%%
%% @param Opts Database configuration map
%% @param Path The path whose parents should exist
%% @returns ok
-spec ensure_parent_groups(map(), binary() | [binary()]) -> ok.
ensure_parent_groups(Opts, Path) when is_binary(Path) ->
    ensure_parent_groups(Opts, [Path]);
ensure_parent_groups(Opts, Paths) when is_list(Paths) ->
    % Collect the unique set of ancestor group paths across all of the given
    % paths, then create each at most once: a single existence check and write
    % per group for the whole batch, rather than once per path. Links that share
    % ancestors (e.g. every key of a message under its id) thus pay for their
    % parent groups only once.
    Groups =
        lists:usort(
            lists:foldl(
                fun(Path, Acc) -> parent_group_paths(Path, Acc) end,
                [],
                Paths
            )
        ),
    lists:foreach(
        fun(GroupPath) ->
            case read_direct(Opts, GroupPath) of
                not_found -> write(Opts, GroupPath, <<"group">>);
                {ok, _} -> ok
            end
        end,
        Groups
    ).

%% @doc Collect the ancestor group paths of a single path onto an accumulator.
parent_group_paths(Path, Acc) ->
    case binary:split(Path, <<"/">>, [global]) of
        [_] -> Acc;
        Parts -> prefix_group_paths(lists:droplast(Parts), [], Acc)
    end.

prefix_group_paths([], _Current, Acc) ->
    Acc;
prefix_group_paths([Next | Rest], Current, Acc) ->
    NewCurrent = Current ++ [Next],
    Path = hb_store_utils:to_path(NewCurrent),
    prefix_group_paths(Rest, NewCurrent, [Path | Acc]).

%% @doc Create a symbolic link from a new key to an existing key.
%%
%% This function implements a symbolic link mechanism by storing a special
%% "link:" prefixed value at the new key location. When the new key is read,
%% the system will automatically resolve the link and return the value from
%% the target key instead.
%%
%% Links provide a way to create aliases, shortcuts, or alternative access
%% paths to the same underlying data without duplicating storage. They can
%% be chained together to create complex reference structures, though care
%% should be taken to avoid circular references.
%%
%% The link resolution happens transparently during read operations, making
%% links invisible to most application code while providing powerful
%% organizational capabilities.
%%
%% @param StoreOpts Database configuration map
%% @param Existing The key that already exists and contains the target value
%% @param New The new key that should link to the existing key
%% @returns Result of the write operation
link(Opts, Req, _NodeOpts) when is_map(Req) ->
    % Resolve every link to a binary `New => "link:Existing"' value. The parent
    % groups of all the links are created once for the whole batch (de-duplicated
    % across links that share ancestors) rather than re-checked for every link.
    Links =
        maps:fold(
            fun(New, Existing, Acc) ->
                Acc#{
                    hb_path:to_binary(New) =>
                        <<"link:", (hb_path:to_binary(Existing))/binary>>
                }
            end,
            #{},
            Req
        ),
    ensure_parent_groups(Opts, maps:keys(Links)),
    write(Opts, Links, #{});
link(#{ <<"read-only">> := true }, _Existing, _New) ->
    {error, not_found};
link(Opts, Existing, New) when is_list(Existing) ->
    link(Opts, hb_store_utils:to_path(Existing), New);
link(Opts, Existing, New) ->
   ExistingBin = hb_util:bin(Existing),
   ensure_parent_groups(Opts, hb_path:to_binary(New)),
   write(Opts, hb_path:to_binary(New), <<"link:", ExistingBin/binary>>).

%% @doc Resolve a path by following any symbolic links.
%%
%% For LMDB, we handle links through our own "link:" prefix mechanism.
%% This function resolves link chains in paths, similar to filesystem symlink resolution.
%% It's used by the cache to resolve paths before type checking and reading.
%%
%% @param StoreOpts Database configuration map
%% @param Path The path to resolve (binary or list)
%% @returns The resolved path as a binary
resolve(Opts, #{ <<"resolve">> := Path }, _NodeOpts) ->
    PathBin = hb_path:to_binary(Path),
    case hb_store_utils:resolve_path_links(
        fun(Key) -> read_direct(Opts, Key) end,
        binary:split(PathBin, <<"/">>, [global])
    ) of
        {ok, ResolvedParts} ->
            {ok, hb_store_utils:to_path(ResolvedParts)};
        {error, _} ->
            {ok, PathBin}
    end.

%% @doc Retrieve or create the LMDB environment handle for a database.
find_env(Opts = #{ <<"db">> := _ }) -> Opts;
find_env(Opts) -> hb_store:find(Opts).

ensure_env(Opts = #{ <<"db">> := _ }) -> Opts;
ensure_env(Opts) -> maps:merge(Opts, find_env(Opts)).

%% Shutdown LMDB environment and cleanup resources
stop(#{ <<"store-module">> := ?MODULE, <<"name">> := DataDir }, _Req, _Opts) ->
    % Soft-close by name; refs stay valid and reopen lazily on next access.
    catch elmdb:env_close_by_name(hb_util:list(DataDir)),
    ok;
stop(_InvalidStoreOpts, _Req, _Opts) ->
    ok.

%% @doc Completely delete the database directory and all its contents.
%%
%% This is a destructive operation that removes all data from the specified
%% database. It first performs a graceful shutdown to ensure data consistency,
%% then uses the system shell to recursively delete the entire database
%% directory structure.
%%
%% This function is primarily intended for testing and development scenarios
%% where you need to start with a completely clean database state. It should
%% be used with extreme caution in production environments.
%%
%% @param StoreOpts Database configuration map containing the directory prefix
%% @returns 'ok' when deletion is complete
reset(Opts, _Req, _NodeOpts) ->
    case maps:get(<<"name">>, Opts, undefined) of
        undefined ->
            % No prefix specified, nothing to reset
            ok;
        DataDir ->
            % Stop the store and remove the database.
            stop(Opts, #{}, #{}),
            os:cmd(binary_to_list(<< "rm -Rf ", DataDir/binary >>)),
            ensure_dir(DataDir),
            ok
    end.

%% @doc Sample roughly 1/1024 reads using the start timestamp and scale the
%% hit counter by the same factor to preserve an approximate total.
sample_metrics(_Name, StartTime, _Type) when (StartTime band 1023) =/= 0 ->
    ok;
sample_metrics(Name, StartTime, Type) ->
    ReadTime = erlang:monotonic_time() - StartTime,
    hb_prometheus:observe(ReadTime, hb_store_lmdb_duration_seconds, [read, Name]),
    case Type of
        hit -> hb_prometheus:inc(counter, hb_store_lmdb_hit, [Name], 1024);
        miss -> ok;
        error -> ok
    end.

init_prometheus() ->
    hb_prometheus:declare(histogram, [
        {name, hb_store_lmdb_duration_seconds},
        {labels, [function, store_name]},
        {buckets, [0.001, 0.005, 0.01, 0.05, 0.1, 0.5, 1, 5, 10, 20]},
        {help, "Duration of lmdb operations in microseconds"}
    ]),
    hb_prometheus:declare(counter, [
        {name, hb_store_lmdb_hit},
        {labels, [name]},
        {help, "LMDB name requested"}
    ]),
    ok.

%% @doc Test suite demonstrating basic store operations.
%%
%% The following functions implement unit tests using EUnit to verify that
%% the LMDB store implementation correctly handles various scenarios including
%% basic read/write operations, hierarchical listing, group creation, link
%% resolution, and type detection.

test_reset(StoreOpts) ->
    reset(StoreOpts, #{}, #{}).

test_stop(StoreOpts) ->
    stop(StoreOpts, #{}, #{}).

test_group(StoreOpts, Path) ->
    write(StoreOpts, hb_path:to_binary(Path), <<"group">>).

test_link(StoreOpts, Existing, New) ->
    link(StoreOpts, Existing, New).

test_type(StoreOpts, Path) ->
    case read_resolved(StoreOpts, hb_path:to_binary(Path)) of
        {ok, _ResolvedPath, <<"group">>} -> composite;
        {ok, _ResolvedPath, _Value} -> simple;
        not_found -> not_found
    end.

test_read(StoreOpts, Path) ->
    case read_resolved(StoreOpts, hb_path:to_binary(Path)) of
        {ok, _ResolvedPath, <<"group">>} -> not_found;
        {ok, _ResolvedPath, Value} -> {ok, Value};
        not_found -> not_found
    end.

test_list(StoreOpts, Path) ->
    PathBin = hb_path:to_binary(Path),
    ResolvedPath =
        case read_direct(StoreOpts, PathBin) of
            {ok, Value} ->
                case hb_store_utils:is_link(Value) of
                    {true, Link} -> Link;
                    false -> PathBin
                end;
            not_found ->
                PathBin
        end,
    list_children(StoreOpts, ResolvedPath, #{}).

test_write(StoreOpts, Path, Value) ->
    ok = write(StoreOpts, Path, Value),
    ok.

%% @doc Basic store test - verifies fundamental read/write functionality.
%%
%% This test creates a temporary database, writes a key-value pair, reads it
%% back to verify correctness, and cleans up by stopping the database. It
%% serves as a sanity check that the basic storage mechanism is working.
basic_test() ->
    StoreOpts = #{
        <<"store-module">> => ?MODULE,
        <<"name">> => <<"/tmp/store-1">>
    },
    test_reset(StoreOpts),
    Res = test_write(StoreOpts, <<"Hello">>, <<"World2">>),
    ?assertEqual(ok, Res),
    {ok, Value} = test_read(StoreOpts, <<"Hello">>),
    ?assertEqual(Value, <<"World2">>),
    ok = test_stop(StoreOpts).

%% @doc List test - verifies prefix-based key listing functionality.
%%
%% This test creates several keys with hierarchical names and verifies that
%% the list operation correctly returns only keys matching a specific prefix.
%% It demonstrates the directory-like navigation capabilities of the store.
list_test() ->
    StoreOpts = #{
        <<"store-module">> => ?MODULE,
        <<"name">> => <<"/tmp/store-2">>,
        <<"capacity">> => ?DEFAULT_SIZE
    },
    test_reset(StoreOpts),
    ?assertEqual({ok, []}, test_list(StoreOpts, <<"colors">>)),
    % Create immediate children under colors/
    test_write(StoreOpts, <<"colors/red">>, <<"1">>),
    test_write(StoreOpts, <<"colors/blue">>, <<"2">>),
    test_write(StoreOpts, <<"colors/green">>, <<"3">>),
    % Create nested directories under colors/ - these should show up as immediate children
    test_write(StoreOpts, <<"colors/multi/foo">>, <<"4">>),
    test_write(StoreOpts, <<"colors/multi/bar">>, <<"5">>),
    test_write(StoreOpts, <<"colors/primary/red">>, <<"6">>),
    test_write(StoreOpts, <<"colors/primary/blue">>, <<"7">>),
    test_write(StoreOpts, <<"colors/nested/deep/value">>, <<"8">>),
    % Create other top-level directories
    test_write(StoreOpts, <<"foo/bar">>, <<"baz">>),
    test_write(StoreOpts, <<"beep/boop">>, <<"bam">>),
    test_read(StoreOpts, <<"colors">>),
    % Test listing colors/ - should return immediate children only
    {ok, ListResult} = test_list(StoreOpts, <<"colors">>),
    ?event_debug({list_result, ListResult}),
    % Expected: red, blue, green (files) + multi, primary, nested (directories)
    % Should NOT include deeply nested items like foo, bar, deep, value
    ExpectedChildren = [<<"blue">>, <<"green">>, <<"multi">>, <<"nested">>, <<"primary">>, <<"red">>],
    ?assert(lists:all(fun(Key) -> lists:member(Key, ExpectedChildren) end, ListResult)),
    % Test listing a nested directory - should only show immediate children
    {ok, NestedListResult} = test_list(StoreOpts, <<"colors/multi">>),
    ?event_debug({nested_list_result, NestedListResult}),
    ExpectedNestedChildren = [<<"bar">>, <<"foo">>],
    ?assert(lists:all(fun(Key) -> lists:member(Key, ExpectedNestedChildren) end, NestedListResult)),
    % Test listing a deeper nested directory
    {ok, DeepListResult} = test_list(StoreOpts, <<"colors/nested">>),
    ?event_debug({deep_list_result, DeepListResult}),
    ExpectedDeepChildren = [<<"deep">>],
    ?assert(lists:all(fun(Key) -> lists:member(Key, ExpectedDeepChildren) end, DeepListResult)),
    ok = test_stop(StoreOpts).

%% @doc Group test - verifies group creation and type detection.
%%
%% This test creates a group entry and verifies that it is correctly identified 
%% as a composite type and cannot be read directly (like filesystem directories).
group_test() ->
    StoreOpts = #{
        <<"store-module">> => ?MODULE,
        <<"name">> => <<"/tmp/store3">>,
        <<"capacity">> => ?DEFAULT_SIZE
    },
    test_reset(StoreOpts),
    test_group(StoreOpts, <<"colors">>),
    % Groups should be detected as composite types
    ?assertEqual(composite, test_type(StoreOpts, <<"colors">>)),
    % Groups should not be readable directly (like directories in filesystem)
    ?assertEqual(not_found, test_read(StoreOpts, <<"colors">>)).

%% @doc Link test - verifies symbolic link creation and resolution.
%%
%% This test creates a regular key-value pair, creates a link pointing to it,
%% and verifies that reading from the link location returns the original value.
%% This demonstrates the transparent link resolution mechanism.
link_test() ->
    StoreOpts = hb_test_utils:test_store(?MODULE),
    test_reset(StoreOpts),
    test_write(StoreOpts, <<"foo/bar/baz">>, <<"Bam">>),
    test_link(StoreOpts, <<"foo/bar/baz">>, <<"foo/beep/baz">>),
    {ok, Result} = test_read(StoreOpts, <<"foo/beep/baz">>),
    ?event_debug({ result, Result}),
    ?assertEqual(<<"Bam">>, Result).

link_fragment_test() ->
    StoreOpts = hb_test_utils:test_store(?MODULE),
    test_reset(StoreOpts),
    test_write(StoreOpts, [<<"data">>, <<"bar">>, <<"baz">>], <<"Bam">>),
    test_link(StoreOpts, [<<"data">>, <<"bar">>], <<"my-link">>),
    {ok, Result} = test_read(StoreOpts, [<<"my-link">>, <<"baz">>]),
    ?event_debug({ result, Result}),
    ?assertEqual(<<"Bam">>, Result).

%% @doc Type test - verifies type detection for both simple and composite entries.
%%
%% This test creates both a group (composite) entry and a regular (simple) entry,
%% then verifies that the type detection function correctly identifies each one.
%% This demonstrates the semantic classification system used by the store.
type_test() ->
    StoreOpts = hb_test_utils:test_store(?MODULE),
    test_reset(StoreOpts),
    test_group(StoreOpts, <<"assets">>),
    Type = test_type(StoreOpts, <<"assets">>),
    ?event_debug({type, Type}),
    ?assertEqual(composite, Type),
    test_write(StoreOpts, <<"assets/1">>, <<"bam">>),
    Type2 = test_type(StoreOpts, <<"assets/1">>),
    ?event_debug({type2, Type2}),
    ?assertEqual(simple, Type2).

%% @doc Link key list test - verifies symbolic link creation using structured key paths.
%%
%% This test demonstrates the store's ability to handle complex key structures
%% represented as lists of binary segments, and verifies that symbolic links
%% work correctly when the target key is specified as a list rather than a
%% flat binary string.
%%
%% The test creates a hierarchical key structure using a list format (which
%% presumably gets converted to a path-like binary internally), creates a
%% symbolic link pointing to that structured key, and verifies that link
%% resolution works transparently to return the original value.
%%
%% This is particularly important for applications that organize data in
%% hierarchical structures where keys represent nested paths or categories,
%% and need to create shortcuts or aliases to deeply nested data.
link_key_list_test() ->
    StoreOpts = hb_test_utils:test_store(?MODULE),
    test_reset(StoreOpts),
    test_write(StoreOpts, [ <<"parent">>, <<"key">> ], <<"value">>),
    test_link(StoreOpts, [ <<"parent">>, <<"key">> ], <<"my-link">>),
    {ok, Result} = test_read(StoreOpts, <<"my-link">>),
    ?event_debug({result, Result}),
    ?assertEqual(<<"value">>, Result).

%% @doc Path traversal link test - verifies link resolution during path traversal.
%%
%% This test verifies that when reading a path as a list, intermediate path
%% segments that are links get resolved correctly. For example, if "link" 
%% is a symbolic link to "group", then reading ["link", "key"] should 
%% resolve to reading ["group", "key"].
%%
%% This functionality enables transparent redirection at the directory level,
%% allowing reorganization of hierarchical data without breaking existing
%% access patterns.
path_traversal_link_test() ->
    StoreOpts = hb_test_utils:test_store(?MODULE),
    test_reset(StoreOpts),
    % Create the actual data at group/key
    test_write(StoreOpts, [<<"group">>, <<"key">>], <<"target-value">>),
    % Create a link from "link" to "group"
    test_link(StoreOpts, <<"group">>, <<"link">>),
    % Reading via the link path should resolve to the target value
    {ok, Result} = test_read(StoreOpts, [<<"link">>, <<"key">>]),
    ?event_debug({path_traversal_result, Result}),
    ?assertEqual(<<"target-value">>, Result),
    ok = test_stop(StoreOpts).

%% @doc Test that matches the exact hb_store hierarchical test pattern
exact_hb_store_test() ->
    StoreOpts = hb_test_utils:test_store(?MODULE),
    % Follow exact same pattern as hb_store test
    ?event(step1_make_group),
    test_group(StoreOpts, <<"test-dir1">>),
    ?event(step2_write_file),
    test_write(StoreOpts, [<<"test-dir1">>, <<"test-file">>], <<"test-data">>),
    ?event(step3_make_link),
    test_link(StoreOpts, [<<"test-dir1">>], <<"test-link">>),
    % Debug: test that the link behaves like the target (groups are unreadable)
    ?event(step4_check_link),
    LinkResult = test_read(StoreOpts, <<"test-link">>),
    ?event_debug({link_result, LinkResult}),
    % Since test-dir1 is a group and groups are unreadable, the link should also be unreadable
    ?assertEqual(not_found, LinkResult),
    % Debug: test intermediate steps
    ?event(step5_test_direct_read),
    _DirectResult = test_read(StoreOpts, <<"test-dir1/test-file">>),
    ?event_debug({direct_result, _DirectResult}),
    % This should work: reading via the link path  
    ?event(step6_test_link_read),
    Result = test_read(StoreOpts, [<<"test-link">>, <<"test-file">>]),
    ?event_debug({final_result, Result}),
    ?assertEqual({ok, <<"test-data">>}, Result),
    ok = test_stop(StoreOpts).

%% @doc Test cache-style usage through hb_store interface
cache_style_test() ->
    hb:init(),
    StoreOpts = hb_test_utils:test_store(?MODULE),
    test_reset(StoreOpts),
    % Start the store
    hb_store:start(StoreOpts),
    % Test writing through hb_store interface  
    ok = hb_store:write(StoreOpts, #{ <<"test-key">> => <<"test-value">> }, #{}),
    % Test reading through hb_store interface
    Result = hb_store:read(StoreOpts, <<"test-key">>, #{}),
    ?event_debug({cache_style_read_result, Result}),
    ?assertEqual({ok, <<"test-value">>}, Result),
    hb_store:stop(StoreOpts).

%% @doc Test nested map storage with cache-like linking behavior
%%
%% This test demonstrates how to store a nested map structure where:
%% 1. Each value is stored at data/{hash_of_value} 
%% 2. Links are created to compose the values back into the original map structure
%% 3. Reading the composed structure reconstructs the original nested map
nested_map_cache_test() ->
    StoreOpts = hb_test_utils:test_store(?MODULE),
    % Clean up any previous test data
    test_reset(StoreOpts),
    % Original nested map structure
    OriginalMap = #{
        <<"target">> => <<"Foo">>,
        <<"commitments">> => #{
            <<"key1">> => #{
              <<"alg">> => <<"rsa-pss-512">>,
              <<"committer">> => <<"unique-id">>
            },
            <<"key2">> => #{
              <<"alg">> => <<"hmac">>,
              <<"commiter">> => <<"unique-id-2">>              
            }
        },
        <<"other-key">> => #{
            <<"other-key-key">> => <<"other-key-value">>
        }
    },
    ?event_debug({original_map, OriginalMap}),
    % Step 1: Store each leaf value at data/{hash}
    TargetValue = <<"Foo">>,
    TargetHash = base64:encode(crypto:hash(sha256, TargetValue)),
    test_write(StoreOpts, <<"data/", TargetHash/binary>>, TargetValue),
    AlgValue1 = <<"rsa-pss-512">>,
    AlgHash1 = base64:encode(crypto:hash(sha256, AlgValue1)),
    test_write(StoreOpts, <<"data/", AlgHash1/binary>>, AlgValue1),
    CommitterValue1 = <<"unique-id">>,
    CommitterHash1 = base64:encode(crypto:hash(sha256, CommitterValue1)),
    test_write(StoreOpts, <<"data/", CommitterHash1/binary>>, CommitterValue1),
    AlgValue2 = <<"hmac">>,
    AlgHash2 = base64:encode(crypto:hash(sha256, AlgValue2)),
    test_write(StoreOpts, <<"data/", AlgHash2/binary>>, AlgValue2),
    CommitterValue2 = <<"unique-id-2">>,
    CommitterHash2 = base64:encode(crypto:hash(sha256, CommitterValue2)),
    test_write(StoreOpts, <<"data/", CommitterHash2/binary>>, CommitterValue2),
    OtherKeyValue = <<"other-key-value">>,
    OtherKeyHash = base64:encode(crypto:hash(sha256, OtherKeyValue)),
    test_write(StoreOpts, <<"data/", OtherKeyHash/binary>>, OtherKeyValue),
    % Step 2: Create the nested structure with groups and links
    % Create the root group
    test_group(StoreOpts, <<"root">>),
    % Create links for the root level keys
    test_link(StoreOpts, <<"data/", TargetHash/binary>>, <<"root/target">>),
    % Create the commitments subgroup
    test_group(StoreOpts, <<"root/commitments">>),
    % Create the key1 subgroup within commitments
    test_group(StoreOpts, <<"root/commitments/key1">>),
    test_link(StoreOpts, <<"data/", AlgHash1/binary>>, <<"root/commitments/key1/alg">>),
    test_link(StoreOpts, <<"data/", CommitterHash1/binary>>, <<"root/commitments/key1/committer">>),
    % Create the key2 subgroup within commitments
    test_group(StoreOpts, <<"root/commitments/key2">>),
    test_link(StoreOpts, <<"data/", AlgHash2/binary>>, <<"root/commitments/key2/alg">>),
    test_link(StoreOpts, <<"data/", CommitterHash2/binary>>, <<"root/commitments/key2/commiter">>),
    % Create the other-key subgroup
    test_group(StoreOpts, <<"root/other-key">>),
    test_link(StoreOpts, <<"data/", OtherKeyHash/binary>>, <<"root/other-key/other-key-key">>),
    % Step 3: Test reading the structure back
    % Verify the root is a composite
    ?assertEqual(composite, test_type(StoreOpts, <<"root">>)),
    % List the root contents
    {ok, RootKeys} = test_list(StoreOpts, <<"root">>),
    ?event_debug({root_keys, RootKeys}),
    ExpectedRootKeys = [<<"commitments">>, <<"other-key">>, <<"target">>],
    ?assert(lists:all(fun(Key) -> lists:member(Key, ExpectedRootKeys) end, RootKeys)),
    % Read the target directly
    {ok, TargetValueRead} = test_read(StoreOpts, <<"root/target">>),
    ?assertEqual(<<"Foo">>, TargetValueRead),
    % Verify commitments is a composite
    ?assertEqual(composite, test_type(StoreOpts, <<"root/commitments">>)),
    % Verify other-key is a composite  
    ?assertEqual(composite, test_type(StoreOpts, <<"root/other-key">>)),
    % Step 4: Test programmatic reconstruction of the nested map
    ReconstructedMap = reconstruct_map(StoreOpts, <<"root">>),
    ?event_debug({reconstructed_map, ReconstructedMap}),
    % Verify the reconstructed map matches the original structure
    ?assert(hb_message:match(OriginalMap, ReconstructedMap)),
    test_stop(StoreOpts).

%% Helper function to recursively reconstruct a map from the store
reconstruct_map(StoreOpts, Path) ->
    case test_type(StoreOpts, Path) of
        composite ->
            % This is a group, reconstruct it as a map
            {ok, ImmediateChildren} = test_list(StoreOpts, Path),
            % The list function now correctly returns only immediate children
            ?event_debug({path, Path, immediate_children, ImmediateChildren}),
            maps:from_list([
                {Key, reconstruct_map(StoreOpts, <<Path/binary, "/", Key/binary>>)}
                || Key <- ImmediateChildren
            ]);
        simple ->
            % This is a simple value, read it directly
            {ok, Value} = test_read(StoreOpts, Path),
            Value;
        not_found ->
            % Path doesn't exist
            undefined
    end.

%% @doc Debug test to understand cache linking behavior
cache_debug_test() ->
    StoreOpts = hb_test_utils:test_store(?MODULE),
    test_reset(StoreOpts),
    % Simulate what the cache does:
    % 1. Create a group for message ID
    MessageID = <<"test_message_123">>,
    test_group(StoreOpts, MessageID),
    % 2. Store a value at data/hash
    Value = <<"test_value">>,
    ValueHash = base64:encode(crypto:hash(sha256, Value)),
    DataPath = <<"data/", ValueHash/binary>>,
    test_write(StoreOpts, DataPath, Value),
    % 3. Calculate a key hashpath (simplified version)
    KeyHashPath = <<MessageID/binary, "/", "key_hash_abc">>,
    % 4. Create link from data path to key hash path
    test_link(StoreOpts, DataPath, KeyHashPath),
    % 5. Test what the cache would see:
    ?event_debug(debug_cache_test, {step, check_message_type}),
    _MsgType = test_type(StoreOpts, MessageID),
    ?event_debug(debug_cache_test, {message_type, _MsgType}),
    ?event_debug(debug_cache_test, {step, list_message_contents}),
    {ok, _Subkeys} = test_list(StoreOpts, MessageID),
    ?event_debug(debug_cache_test, {message_subkeys, _Subkeys}),
    ?event_debug(debug_cache_test, {step, read_key_hashpath}),
    _KeyHashResult = test_read(StoreOpts, KeyHashPath),
    ?event_debug(debug_cache_test, {key_hash_read_result, _KeyHashResult}),
    % 6. Test with path as list (what cache does):
    ?event_debug(debug_cache_test, {step, read_path_as_list}),
    PathAsList = [MessageID, <<"key_hash_abc">>],
    _PathAsListResult = test_read(StoreOpts, PathAsList),
    ?event_debug(debug_cache_test, {path_as_list_result, _PathAsListResult}),
    test_stop(StoreOpts).

%% @doc Isolated test focusing on the exact cache issue
isolated_type_debug_test() ->
    StoreOpts = hb_test_utils:test_store(?MODULE),
    test_reset(StoreOpts),
    % Create the exact scenario from user's description:
    % 1. A message ID with nested structure
    MessageID = <<"Base23">>,
    test_group(StoreOpts, MessageID),
    % 2. Create nested groups for "commitments" and "other-test-key"
    CommitmentsPath = <<MessageID/binary, "/commitments">>,
    OtherKeyPath = <<MessageID/binary, "/other-test-key">>,
    ?event_debug(debug_isolated, {creating_nested_groups, CommitmentsPath, OtherKeyPath}),
    test_group(StoreOpts, CommitmentsPath),
    test_group(StoreOpts, OtherKeyPath),
    % 3. Add some actual data within those groups
    test_write(StoreOpts, <<CommitmentsPath/binary, "/sig1">>, <<"signature_data_1">>),
    test_write(StoreOpts, <<OtherKeyPath/binary, "/sub_value">>, <<"nested_value">>),
    % 4. Test type detection on the nested paths
    ?event_debug(debug_isolated, {testing_main_message_type}),
    _MainType = test_type(StoreOpts, MessageID),
    ?event_debug(debug_isolated, {main_message_type, _MainType}),
    ?event_debug(debug_isolated, {testing_commitments_type}),
    _CommitmentsType = test_type(StoreOpts, CommitmentsPath),
    ?event_debug(debug_isolated, {commitments_type, _CommitmentsType}),
    ?event_debug(debug_isolated, {testing_other_key_type}),
    _OtherKeyType = test_type(StoreOpts, OtherKeyPath),
    ?event_debug(debug_isolated, {other_key_type, _OtherKeyType}),
    % 5. Test what happens when reading these nested paths
    ?event_debug(debug_isolated, {reading_commitments_directly}),
    _CommitmentsResult = test_read(StoreOpts, CommitmentsPath),
    ?event_debug(debug_isolated, {commitments_read_result, _CommitmentsResult}),
    ?event_debug(debug_isolated, {reading_other_key_directly}),
    _OtherKeyResult = test_read(StoreOpts, OtherKeyPath),
    ?event_debug(debug_isolated, {other_key_read_result, _OtherKeyResult}),
    test_stop(StoreOpts).

%% @doc Test that list function resolves links correctly
list_with_link_test() ->
    StoreOpts = hb_test_utils:test_store(?MODULE),
    test_reset(StoreOpts),
    % Create a group with some children
    test_group(StoreOpts, <<"real-group">>),
    test_write(StoreOpts, <<"real-group/child1">>, <<"value1">>),
    test_write(StoreOpts, <<"real-group/child2">>, <<"value2">>),
    test_write(StoreOpts, <<"real-group/child3">>, <<"value3">>),
    % Create a link to the group
    test_link(StoreOpts, <<"real-group">>, <<"link-to-group">>),
    % List the real group to verify expected children
    {ok, RealGroupChildren} = test_list(StoreOpts, <<"real-group">>),
    ?event_debug({real_group_children, RealGroupChildren}),
    ExpectedChildren = [<<"child1">>, <<"child2">>, <<"child3">>],
    ?assertEqual(ExpectedChildren, lists:sort(RealGroupChildren)),
    % List via the link - should return the same children
    {ok, LinkChildren} = test_list(StoreOpts, <<"link-to-group">>),
    ?event_debug({link_children, LinkChildren}),
    ?assertEqual(ExpectedChildren, lists:sort(LinkChildren)),
    test_stop(StoreOpts).

read_prefix_composite_test() ->
    StoreOpts = hb_test_utils:test_store(?MODULE),
    test_reset(StoreOpts),
    test_group(StoreOpts, <<"root">>),
    test_write(StoreOpts, <<"root/a">>, <<"1">>),
    test_group(StoreOpts, <<"root/b">>),
    test_write(StoreOpts, <<"root/b/c">>, <<"2">>),
    ?assertEqual(
        {composite, [{<<"a">>, <<"1">>}, {<<"b">>, <<"group">>}]},
        read(StoreOpts, #{ <<"read">> => <<"root">> }, #{})
    ),
    ?assertEqual({ok, [<<"a">>, <<"b">>]}, test_list(StoreOpts, <<"root">>)),
    test_stop(StoreOpts).

%% @doc A sync commits every write that has already returned, at either level,
%% and concurrent callers are all answered.
sync_commits_overlay_test() ->
    StoreOpts = hb_test_utils:test_store(?MODULE),
    test_reset(StoreOpts),
    #{ <<"db">> := DB } = ensure_env(StoreOpts),
    lists:foreach(
        fun(N) -> test_write(StoreOpts, <<"key-", (hb_util:bin(N))/binary>>, <<"v">>) end,
        lists:seq(1, 10)
    ),
    ?assert(elmdb:overlay_count(DB) > 0),
    ?assertEqual(ok, hb_store:sync(StoreOpts, #{})),
    ?assertEqual(0, elmdb:overlay_count(DB)),
    Self = self(),
    Callers =
        [
            spawn(
                fun() ->
                    test_write(StoreOpts, <<"par-", (hb_util:bin(N))/binary>>, <<"v">>),
                    Self ! {done, self(), hb_store:sync(StoreOpts, #{ <<"level">> => fsync }, #{})}
                end
            )
        ||
            N <- lists:seq(1, 20)
        ],
    lists:foreach(
        fun(Caller) -> receive {done, Caller, Res} -> ?assertEqual(ok, Res) end end,
        Callers
    ),
    ?assertEqual(0, elmdb:overlay_count(DB)),
    ?assertEqual({ok, <<"v">>}, test_read(StoreOpts, <<"par-7">>)),
    test_stop(StoreOpts).

%%% Delete primitive (patches/elmdb-delete.patch).

delete_env() ->
    StoreOpts = hb_test_utils:test_store(?MODULE),
    test_reset(StoreOpts),
    #{ <<"db">> := DB } = ensure_env(StoreOpts),
    {StoreOpts, DB}.

%% @doc A committed key is gone after a delete, and deleting it again (or a key
%% that never existed) is not an error.
delete_committed_key_test() ->
    {StoreOpts, DB} = delete_env(),
    test_write(StoreOpts, <<"k1">>, <<"v1">>),
    test_write(StoreOpts, <<"k2">>, <<"v2">>),
    ok = elmdb:flush(DB),
    ?assertEqual({ok, 1}, hb_store:delete(StoreOpts, [<<"k1">>], #{})),
    ?assertEqual(not_found, elmdb:get(DB, <<"k1">>)),
    ?assertEqual({ok, <<"v2">>}, elmdb:get(DB, <<"k2">>)),
    ?assertEqual({ok, 0}, hb_store:delete(StoreOpts, [<<"k1">>, <<"never">>], #{})),
    ?assertEqual({error, not_found}, read(StoreOpts, #{ <<"read">> => <<"k1">> }, #{})),
    test_stop(StoreOpts).

%% @doc A put still in the overlay when the delete arrives is committed and
%% deleted, never resurrected by a later flush; a put after the delete wins.
delete_beats_pending_overlay_put_test() ->
    {StoreOpts, DB} = delete_env(),
    test_write(StoreOpts, <<"pending">>, <<"v">>),
    ?assert(elmdb:overlay_count(DB) > 0),
    ?assertEqual({ok, 1}, elmdb:delete(DB, <<"pending">>)),
    ?assertEqual(not_found, elmdb:get(DB, <<"pending">>)),
    ok = elmdb:flush(DB),
    ?assertEqual(not_found, elmdb:get(DB, <<"pending">>)),
    test_write(StoreOpts, <<"pending">>, <<"again">>),
    ?assertEqual({ok, <<"again">>}, elmdb:get(DB, <<"pending">>)),
    ok = elmdb:flush(DB),
    ?assertEqual({ok, <<"again">>}, elmdb:get(DB, <<"pending">>)),
    test_stop(StoreOpts).

%% @doc A batch deletes exactly the keys named and nothing else.
delete_batch_is_exact_test() ->
    {StoreOpts, DB} = delete_env(),
    Keys = [ <<"b/", (integer_to_binary(N))/binary>> || N <- lists:seq(1, 2000) ],
    ok = elmdb:put_batch(DB, [ {K, K} || K <- Keys ]),
    {Gone, Kept} = lists:split(1000, Keys),
    ?assertEqual({ok, 1000}, elmdb:delete_batch(DB, Gone)),
    ?assertEqual([not_found], lists:usort([ elmdb:get(DB, K) || K <- Gone ])),
    ?assert(lists:all(fun(K) -> elmdb:get(DB, K) =:= {ok, K} end, Kept)),
    % The parent prefix is still listed with exactly the survivors.
    {ok, Rows} = elmdb:read_prefix(DB, <<"b/">>),
    ?assertEqual(1000, length(Rows)),
    test_stop(StoreOpts).

%% @doc Readers running through a delete never see a deleted key come back,
%% and never lose a key that was not deleted.
delete_with_concurrent_readers_test_() ->
    {timeout, 60, fun() ->
        {StoreOpts, DB} = delete_env(),
        Keep = [ <<"keep/", (integer_to_binary(N))/binary>> || N <- lists:seq(1, 500) ],
        Drop = [ <<"drop/", (integer_to_binary(N))/binary>> || N <- lists:seq(1, 500) ],
        ok = elmdb:put_batch(DB, [ {K, <<"v">>} || K <- Keep ++ Drop ]),
        ok = elmdb:flush(DB),
        Self = self(),
        Reader =
            fun Loop(Seen) ->
                receive stop -> Self ! {reader_done, self(), ok}
                after 0 ->
                    lists:foreach(
                        fun(K) -> {ok, <<"v">>} = elmdb:get(DB, K) end,
                        lists:sublist(Keep, rand:uniform(450), 50)
                    ),
                    Seen2 =
                        lists:foldl(
                            fun(K, Acc) ->
                                case {elmdb:get(DB, K), sets:is_element(K, Acc)} of
                                    {not_found, _} -> sets:add_element(K, Acc);
                                    {{ok, _}, true} -> exit({resurrected, K});
                                    {{ok, _}, false} -> Acc
                                end
                            end,
                            Seen,
                            lists:sublist(Drop, rand:uniform(450), 50)
                        ),
                    Loop(Seen2)
                end
            end,
        Readers =
            [ spawn_link(fun() -> Reader(sets:new()) end) || _ <- lists:seq(1, 8) ],
        % Writers keep the overlay busy with unrelated keys meanwhile.
        WriteLoop =
            fun W(N) ->
                receive stop -> ok
                after 0 ->
                    ok = elmdb:put(DB, <<"other/", (integer_to_binary(N))/binary>>, <<"x">>),
                    W(N + 1)
                end
            end,
        Writer = spawn_link(fun() -> WriteLoop(0) end),
        lists:foreach(
            fun(Chunk) -> {ok, _} = elmdb:delete_batch(DB, Chunk), timer:sleep(5) end,
            chunks(Drop, 50)
        ),
        Writer ! stop,
        [ R ! stop || R <- Readers ],
        [ receive {reader_done, R, ok} -> ok after 10000 -> exit(reader_hung) end
        || R <- Readers ],
        ?assertEqual([not_found], lists:usort([ elmdb:get(DB, K) || K <- Drop ])),
        ?assertEqual([{ok, <<"v">>}], lists:usort([ elmdb:get(DB, K) || K <- Keep ])),
        test_stop(StoreOpts)
    end}.

chunks([], _N) -> [];
chunks(L, N) when length(L) =< N -> [L];
chunks(L, N) -> {A, B} = lists:split(N, L), [A | chunks(B, N)].

%% @doc With tracking on, a guarded delete is vetoed atomically by a write of
%% the key, or by a write whose value references it (a `link:' or a bare ID);
%% an unguarded delete is not.
delete_guarded_by_tracked_writes_test() ->
    {StoreOpts, DB} = delete_env(),
    ID = hb_util:human_id(crypto:strong_rand_bytes(32)),
    ok = elmdb:put_batch(DB, [{<<"t1">>, <<"v">>}, {<<"t2/x">>, <<"v">>}, {ID, <<"v">>}]),
    ok = elmdb:flush(DB),
    ok = elmdb:track(DB, true),
    ?assertEqual({ok, 1}, elmdb:delete_batch_guarded(DB, [<<"t1">>])),
    ok = elmdb:put(DB, <<"ref">>, <<"link:t2/x">>),
    ?assertMatch({error, conflict, [<<"t2/x">>]},
        elmdb:delete_batch_guarded(DB, [<<"t2/x">>])),
    ?assertEqual({ok, <<"v">>}, elmdb:get(DB, <<"t2/x">>)),
    % The prefix of a link target is recorded too.
    {ok, Tracked} = elmdb:track_take(DB),
    ?assert(lists:member(<<"t2">>, Tracked)),
    ?assert(lists:member(<<"ref">>, Tracked)),
    % After a take, only new writes veto.
    ?assertEqual({ok, 1}, elmdb:delete_batch_guarded(DB, [<<"t2/x">>])),
    ok = elmdb:put(DB, <<"idref">>, ID),
    ?assertMatch({error, conflict, [ID]}, elmdb:delete_batch_guarded(DB, [ID])),
    ?assertEqual({ok, 1}, elmdb:delete_batch(DB, [ID])),
    ok = elmdb:track(DB, false),
    ok = elmdb:put(DB, <<"later">>, <<"link:never">>),
    ?assertEqual({ok, 0}, elmdb:delete_batch_guarded(DB, [<<"never">>])),
    test_stop(StoreOpts).

%% @doc `scan_refs' walks the committed keyspace in bounded steps and returns
%% only the rows whose value references a key.
scan_refs_returns_reference_rows_test() ->
    {StoreOpts, DB} = delete_env(),
    ID = hb_util:human_id(crypto:strong_rand_bytes(32)),
    ok = elmdb:put_batch(DB,
        [{<<"a">>, <<"plain">>}, {<<"b">>, <<"link:a">>}, {<<"c">>, ID},
         {<<"d">>, <<"group">>}, {<<"e">>, <<"link:d/x">>}]),
    ok = elmdb:flush(DB),
    Walk =
        fun W(From, Acc, Steps) ->
            case elmdb:scan_refs(DB, From, 2) of
                {ok, Rows, _N, done} -> {Acc ++ Rows, Steps + 1};
                {ok, Rows, 2, Next} -> W(Next, Acc ++ Rows, Steps + 1)
            end
        end,
    {Refs, Steps} = Walk(<<>>, [], 0),
    ?assertEqual([{<<"b">>, <<"link:a">>}, {<<"c">>, ID}, {<<"e">>, <<"link:d/x">>}], Refs),
    ?assertEqual(3, Steps),
    test_stop(StoreOpts).
