%%% @doc A store implementation that relays to an Arweave node, using an 
%%% intermediate cache of offsets as an ID->ArweaveLocation mapping. Stores with
%%% `remote-index' set additionally source the offsets they lack from the nodes
%%% serving the `~arweave@2.9' routes, allowing a node to read data from Arweave
%%% before it has indexed the weave itself.
-module(hb_store_arweave).
%%% Store API:
-export([scope/0, scope/1, type/3, read/3, start/3]).
%%% Unused Store API:
-export([resolve/3, write/3, link/3, group/3]).
%%% Indexing API:
-export([store_from_opts/1, write_offset/5, read_chunks/3]).
-export([read_offset/3, read_index_offset/2]).
-include("include/hb.hrl").
-include_lib("eunit/include/eunit.hrl").

-define(PARTITION_SIZE, 3_600_000_000_000).

%% @doc Find the first Arweave store from the given node message. Searches first
%% for the `arweave_index_store' option, and if not found, searches the main
%% `store' list for the first Arweave store with an index.
store_from_opts(Opts) ->
    case hb_opts:get(arweave_index_store, no_store, Opts) of
        no_store -> first_arweave_store(hb_opts:get(store, [], Opts));
        IndexStoreOpts -> IndexStoreOpts
    end.

%% @doc Find the first Arweave store with an index from a list of stores.
first_arweave_store(NonList) when not is_list(NonList) ->
    first_arweave_store([NonList]);
first_arweave_store([]) -> no_store;
first_arweave_store(
    [Store = #{<<"store-module">> := ?MODULE, <<"index-store">> := _ } | _]
) -> Store;
first_arweave_store([_ | Rest]) -> first_arweave_store(Rest).

%% @doc Start the Arweave store, and the downstream associated index store.
start(#{<<"index-store">> := IndexStore}, _Req, _Opts) ->
    init_prometheus(),
    hb_store:start(IndexStore).

%% @doc Although the index is local, loading an item via the index will make
%% requests to a remote node, so we define the scope as remote.
scope() -> remote.
scope(#{ <<"scope">> := Scope }) -> Scope;
scope(_) -> scope().

%% @doc Resolve a key path in the Arweave store, ignoring other paths.
resolve(_Store, #{ <<"resolve">> := ID }, _NodeOpts) when ?IS_ID(ID) ->
    {ok, ID};
resolve(_Store, #{ <<"resolve">> := _ID }, _NodeOpts) ->
    {error, not_found}.

%% @doc Unsupported.
write(_, _, _) -> {error, not_found}.

%% @doc Unsupported.
link(_, _, _) -> {error, not_found}.

%% @doc Unsupported.
group(_, _, _) -> {error, not_found}.

%% @doc Get the type of the data at the given key. We potentially cache the
%% result, so that we don't have to read the data from the GraphQL route
%% multiple times.
type(StoreOpts, #{ <<"type">> := ID }, _NodeOpts) when ?IS_ID(ID) ->
    case read_index_offset(StoreOpts, ID) of
        {ok, _Offset} -> {ok, simple};
        not_found -> {error, not_found}
    end;
type(_Store, #{ <<"type">> := _ID }, _NodeOpts) ->
    {error, not_found}.

%% @doc Read the offset of the data at the given key.
read_offset(StoreOpts, ID, Opts) ->
    case read_index_offset(StoreOpts, ID) of
        not_found -> read_remote_offset(StoreOpts, ID, Opts);
        Offset -> Offset
    end.

%% @doc Read the offset of the data at the given key from the local index alone.
read_index_offset(StoreOpts = #{ <<"index-store">> := _ }, ID) ->
    maybe
        % First check the full offset path from the `~arweave@2.9` device.
        not_found ?= read_offset_path(StoreOpts, <<"~arweave@2.9/offset=", ID/binary>>),
        % ...otherwise, check the raw binary path for the ID.
        read_offset_path(StoreOpts, hb_store_arweave_offset:path(ID))
    end;
read_index_offset(_, _) -> not_found.

%% @doc Read and parse offset results. Processes both a message response or a
%% version 1 binary encoded response.
read_offset_path(StoreOpts = #{ <<"index-store">> := IndexStore }, Path) ->
    ReadRes =
        hb_prometheus:measure_and_report(
            fun() ->
                hb_store:read(
                    IndexStore,
                    #{ <<"read">> => Path },
                    StoreOpts
                )
            end,
            hb_store_arweave_index_check_duration_seconds
        ),
    case ReadRes of
        {ok, Res} when is_map(Res) ->
            {ok, Res#{
                <<"version">> => maps:get(<<"version">>, Res, 2),
                <<"codec-device">> =>
                    maps:get(
                        <<"codec-device">>,
                        Res,
                        <<"ans104@1.0">>
                    )
            }};
        {ok, OffsetBinary} when is_binary(OffsetBinary) ->
            {Version, CodecName, StartOffset, Length} =
                hb_store_arweave_offset:decode(OffsetBinary),
            {ok, #{
                <<"version">> => Version,
                <<"codec-device">> => CodecName,
                <<"start">> => StartOffset,
                <<"length">> => Length
            }};
        _ ->
            not_found
    end.

%% @doc Find the offset of a message from the nodes serving the `~arweave@2.9'
%% routes, adding it to the local index so that they are asked once per message.
%% The response gives the start of the item alongside the lengths of its header
%% and data segments: an item with no header inside the weave's data tree is a
%% transaction, one with a header is an ANS-104 item, and the two lengths sum to
%% the span that the loader reads. Pending transactions report offsets relative
%% to themselves, so only confirmed items are indexed.
read_remote_offset(
        StoreOpts = #{ <<"index-store">> := _, <<"remote-index">> := true },
        ID,
        Opts
    ) ->
    Res =
        hb_http:request(
            #{
                <<"method">> => <<"HEAD">>,
                <<"path">> => <<"/~arweave@2.9/raw=", ID/binary>>
            },
            Opts
        ),
    ?event(arweave_offsets, {remote_index, {id, {explicit, ID}}, {result, Res}}),
    case Res of
        {ok,
            #{
                <<"offset">> := StartOffset,
                <<"header-length">> := HeaderLength,
                <<"content-length">> := DataLength
            }} when is_integer(StartOffset) ->
            CodecName =
                case HeaderLength of
                    0 -> <<"tx@1.0">>;
                    _ -> <<"ans104@1.0">>
                end,
            write_offset(
                StoreOpts,
                ID,
                CodecName,
                StartOffset,
                HeaderLength + DataLength
            ),
            read_index_offset(StoreOpts, ID);
        _ ->
            not_found
    end;
read_remote_offset(_, _, _) -> not_found.

%% @doc Read the data at the given key, reading the `local-store' first if
%% available.
read(StoreOpts, #{ <<"read">> := ID }, _NodeOpts) when ?IS_ID(ID) ->
    case hb_store_remote_node:read_local_cache(StoreOpts, ID, StoreOpts) of
        {ok, Message} ->
            ?event(
                arweave_offsets,
                {local_store_hit, {id, {explicit, ID}}}
            ),
            {ok, Message};
        _ ->
            case do_read(StoreOpts, ID, StoreOpts) of
                not_found -> {error, not_found};
                Result -> Result
            end
    end;
read(_StoreOpts, #{ <<"read">> := _ID }, _NodeOpts) ->
    {error, not_found}.

%% @doc Read the data at the given key, reading the provided Arweave index store
%% as a source of offsets. After offsets have been found, the data is loaded
%% through the `~arweave@2.9` device -- either as an ANS-104 item or a TX.
do_read(StoreOpts, ID, Opts) ->
    case read_offset(StoreOpts, ID, Opts) of
        {ok,
            #{
                <<"version">> := Version,
                <<"codec-device">> := CodecName,
                <<"start">> := StartOffset,
                <<"length">> := Length
            }} ->
            Loaded =
                case CodecName of
                    <<"ans104@1.0">> -> load_item(ID, StartOffset, Length, Opts);
                    <<"tx@1.0">> -> load_tx(ID, StartOffset, Length, Opts)
                end,
            case Loaded of
                {ok, Message} ->
                    hb_store_remote_node:maybe_cache(StoreOpts, Message),
                    ?event(
                        arweave_offsets,
                        {read_ok,
                            {id, {string, ID}},
                            {format_version, Version},
                            {type, CodecName},
                            {start_offset, StartOffset},
                            {length, Length}
                        }
                    ),
                    record_partition_metric(StartOffset, ok, Opts),
                    Loaded;
                {error, Reason} ->
                    ?event(
                        arweave_offsets,
                        {read_chunks_not_found, 
                            {id, {string, ID}},
                            {format_version, Version},
                            {type, CodecName},
                            {start_offset, StartOffset},
                            {length, Length},
                            {reason, Reason}
                        }
                    ),
                    record_partition_metric(StartOffset, not_found, Opts),
                    if Reason =:= not_found -> not_found;
                    true -> {error, Reason}
                    end
            end;
        not_found ->
            ?event(
                arweave_offsets,
                {miss, {id, {explicit, ID}}}
            ),
            not_found
    end.

%% @doc Load an ANS-104 item from the given start offset and length.
%% Returns an `ok' tuple with the deserialized item, or an `error' tuple with
%% the reason. The `StartOffset` is the precise starting byte of the item _header_,
%% not the data segment. The `Length` covers the full size of the item, including
%% header. The `ExpectedID` is verified against the deserialized item's ID to
%% guard against stale offsets (e.g. after a reorg).
load_item(ExpectedID, StartOffset, Length, Opts) ->
    hb_prometheus:measure_and_report(
        fun() ->
            case read_chunks(StartOffset, Length, Opts) of
                {ok, SerializedItem} ->
                    Item =
                        ar_bundles:deserialize(SerializedItem),
                    case hb_util:encode(Item#tx.id) of
                        ExpectedID ->
                            {ok, hb_message:convert(
                                Item,
                                <<"structured@1.0">>,
                                <<"ans104@1.0">>,
                                Opts
                            )};
                        ActualID ->
                            {error,
                                {id_mismatch,
                                    ExpectedID, ActualID}}
                    end;
                {error, Reason} ->
                    {error, Reason}
            end
        end,
        hb_store_arweave_chunk_fetch_duration_seconds,
        [load_item]
    ).

%% @doc Load a TX from the given start offset and length. The `StartOffset' is
%% the start of the first chunk of the data and runs for the length of the data
%% segment, ignoring header size.
%% Pending TX offsets are fetched by ID.
load_tx(ID, relative, _Length, Opts) ->
    hb_prometheus:measure_and_report(
        fun() ->
            hb_ao:resolve(
                #{ <<"device">> => <<"arweave@2.9">> },
                #{
                    <<"path">> => <<"pending">>,
                    <<"pending">> => ID,
                    <<"exclude-data">> => false
                },
                Opts
            )
        end,
        hb_store_arweave_chunk_fetch_duration_seconds,
        [load_tx]
    );
load_tx(ID, StartOffset, Length, Opts) ->
    hb_prometheus:measure_and_report(
        fun() ->
            {ok, StructuredTXHeader} = hb_ao:resolve(
                #{ <<"device">> => <<"arweave@2.9">> },
                #{
                    <<"path">> => <<"tx">>,
                    <<"tx">> => ID,
                    <<"exclude-data">> => true
                },
                Opts
            ),
            TXHeader =
                hb_message:convert(
                    StructuredTXHeader,
                    <<"tx@1.0">>,
                    <<"structured@1.0">>,
                    Opts
                ),
            case Length of
                0 ->
                    {ok, hb_message:convert(
                        TXHeader,
                        <<"structured@1.0">>,
                        <<"tx@1.0">>,
                        Opts)};
                _ ->
                    case read_chunks(StartOffset, Length, Opts) of
                        {ok, Data} ->
                            {ok, hb_message:convert(
                                TXHeader#tx{data = Data},
                                <<"structured@1.0">>,
                                <<"tx@1.0">>,
                                Opts
                            )};
                        {error, Reason} ->
                            {error, Reason}
                    end
            end
        end,
        hb_store_arweave_chunk_fetch_duration_seconds,
        [load_tx]
    ).

%% @doc Read the chunks from the given start offset and length using the 
%% `~arweave@2.9` device.
read_chunks(#{ <<"relative">> := TXID, <<"offset">> := Offset }, Length, Opts) ->
    hb_ao:resolve(
        #{ <<"device">> => <<"arweave@2.9">> },
        #{
            <<"path">> => <<"chunk">>,
            <<"pending">> => TXID,
            <<"offset">> => Offset,
            <<"length">> => Length
        },
        Opts
    );
read_chunks(StartOffset, Length, Opts) ->
    hb_ao:resolve(
        #{ <<"device">> => <<"arweave@2.9">> },
        #{
            <<"path">> => <<"chunk">>,
            <<"offset">> => StartOffset + 1,
            <<"length">> => Length
        },
        Opts
    ).

%% @doc Write offset information to the index store.
write_offset(
        StoreOpts = #{ <<"index-store">> := IndexStore },
        ID,
        CodecName,
        StartOffset,
        Length
    ) ->
    Value = hb_store_arweave_offset:encode(CodecName, StartOffset, Length),
    ?event(
        debug_store_arweave,
        {writing_offset, 
            {id, {explicit, ID}},
            {type, CodecName},
            {start_offset, StartOffset},
            {length, Length},
            {value, {explicit, Value}}
        }
    ),
    hb_store:write(
        IndexStore,
        #{ hb_store_arweave_offset:path(ID) => Value },
        StoreOpts
    ).

%% @doc Record the partition that data is found in when it is requested.
record_partition_metric(Offset, Result, StoreOpts) when is_integer(Offset) ->
    case hb_opts:get(prometheus, not hb_features:test(), StoreOpts) of
        true ->
            spawn(fun() ->
                hb_prometheus:inc(
                    counter,
                    hb_store_arweave_requests_partition,
                    [Offset div ?PARTITION_SIZE, hb_util:bin(Result)],
                    1
                )
            end);
        false ->
            ok
    end;
record_partition_metric(_, _, _) -> ok.

%% @doc Initialize the Prometheus metrics for the Arweave store. Executed on
%% `start/1' of the store.
init_prometheus() ->
    hb_prometheus:declare(
        histogram,
        [
            {name, hb_store_arweave_index_check_duration_seconds},
            {buckets, [0.001, 0.005, 0.01, 0.05, 0.1, 0.5, 1, 2, 5, 10]},
            {help, "How much it takes to check the index"}
        ]
    ),
    hb_prometheus:declare(
        histogram,
        [
            {name, hb_store_arweave_chunk_fetch_duration_seconds},
            {buckets, [0.001, 0.005, 0.01, 0.05, 0.1, 0.5, 1, 5, 10, 30, 60]},
            {labels, [type]},
            {help, "How much it takes to check the index"}
        ]
    ),
    hb_prometheus:declare(
        counter,
        [
            {name, hb_store_arweave_requests_partition},
            {labels, [partition, result]},
            {help, "Partition where chunks are being requested"}
        ]
    ),
    % We also depend on the HTTP client, so we ensure its prometheus metrics are
    % initialized, too.
    hb_http_client:init_prometheus().

%%% Tests

slash_offset_key_read_test_parallel() ->
    IndexStore = [hb_test_utils:test_store(hb_store_lmdb)],
    Opts = #{
        <<"store">> => [#{
            <<"store-module">> => hb_store_arweave,
            <<"index-store">> => IndexStore
        }]
    },
    Store = store_from_opts(Opts),
    ID = <<"L2FiY2RlZmdoaWprbG0vL29wcXJzdHV2d3h5ejAxMi8">>,
    ?assertEqual(
        <<"/abcdefghijklm//opqrstuvwxyz012/">>,
        hb_store_arweave_offset:path(ID)
    ),
    ok = write_offset(Store, ID, <<"tx@1.0">>, 123, 456),
    ?assertMatch(
        {ok, #{ <<"start">> := 123, <<"length">> := 456 }},
        read_offset(Store, ID, Opts)
    ).

write_read_tx_test() ->
    Store = [hb_test_utils:test_store()],
    Opts = #{ 
        <<"index-store">> => Store 
    },
    ID = <<"bndIwac23-s0K11TLC1N7z472sLGAkiOdhds87ZywoE">>,
    EndOffset = 363524457284025,
    Size = 8387,
    StartOffset = EndOffset - Size,
    ok = write_offset(Opts, ID, <<"tx@1.0">>, StartOffset, Size),
    {ok, Bundle} = read(Opts, #{ <<"read">> => ID }, Opts),
    ?assert(hb_message:verify(Bundle, all, #{})),
    {ok, Child} =
        hb_ao:resolve(
            Bundle,
            <<"1/2">>,
            #{}
        ),
    ?assert(hb_message:verify(Child, all, #{})),
    ExpectedChild = #{
        <<"data">> =>
            <<
                "{\"totalTickedRewardsDistributed\":0,\"distributedEpochIndexes\""
                ":[],\"newDemandFactors\":[],\"newEpochIndexes\":[],\""
                "tickedRewardDistributions\":[],\"newPruneGatewaysResults\""
                ":[{\"delegateStakeReturned\":0,\"stakeSlashed\":0,\""
                "gatewayStakeReturned\":0,\"delegateStakeWithdrawing\":0,\""
                "prunedGateways\":[],\"slashedGateways\":[],\""
                "gatewayStakeWithdrawing\":0}]}">>,
        <<"data-protocol">> => <<"ao">>,
        <<"from-module">> => <<"cbn0KKrBZH7hdNkNokuXLtGryrWM--PjSTBqIzw9Kkk">>,
        <<"from-process">> => <<"agYcCFJtrMG6cqMuZfskIkFTGvUPddICmtQSBIoPdiA">>,
        <<"anchor">> => <<"MDAwMDAwMDAwMDAwMDAwMDAwMDAwMDAwMDAyODAxODg">>,
        <<"reference">> => <<"280188">>,
        <<"target">> => <<"1R5QEtX53Z_RRQJwzFWf40oXiPW2FibErT_h02pu8MU">>,
        <<"type">> => <<"Message">>,
        <<"variant">> => <<"ao.TN.1">>
    },
    ?assert(hb_message:match(ExpectedChild, Child, only_present)),
    ok.

%% @doc Stale ANS-104 offset: fake ID pointing to a known bundle TX's
%% data range. The deserialized item's ID won't match the fake ID.
stale_ans104_offset_returns_error_test() ->
    Store = [hb_test_utils:test_store()],
    Opts = #{<<"index-store">> => Store},
    FakeID = <<"AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA">>,
    RealEndOffset = 363524457284025,
    RealSize = 8387,
    RealStartOffset = RealEndOffset - RealSize,
    ok = write_offset(Opts, FakeID, <<"ans104@1.0">>, RealStartOffset, RealSize),
    Result = read(Opts, #{ <<"read">> => FakeID }, Opts),
    ?assertMatch({error, {id_mismatch, _, _}}, Result).

%% @doc The L1 TX has bundle tags, but data is not a valid bundle.
write_read_fake_bundle_tx_test() ->
    Store = [hb_test_utils:test_store()],
    Opts = #{ 
        <<"index-store">> => Store 
    },
    ID = <<"cGNURX2IUt98VKVIeXSfYe6eulNwPEqijaQfvatzd_o">>,
    Size = 2,
    StartOffset = 155309918167286,
    ok = write_offset(Opts, ID, <<"tx@1.0">>, StartOffset, Size),
    {ok, TX} = read(Opts, #{ <<"read">> => ID }, Opts),
    ?assert(hb_message:verify(TX, all, #{})),
    ok.

%% @doc A node with no offset index of its own finds the offsets of an Arweave
%% transaction and an ANS-104 item from its routes, serving the item over HTTP
%% and retaining both offsets for subsequent reads.
remote_index_test() ->
    Local =
        #{
            <<"store-module">> => hb_store_arweave,
            <<"index-store">> => [hb_test_utils:test_store()]
        },
    Store = Local#{ <<"remote-index">> => true },
    TXID = <<"bndIwac23-s0K11TLC1N7z472sLGAkiOdhds87ZywoE">>,
    ItemID = <<"1R5QEtX53Z_RRQJwzFWf40oXiPW2FibErT_h02pu8MU">>,
    ?assertEqual(not_found, read_offset(Local, TXID, #{})),
    ?assertMatch(
        {ok, #{ <<"codec-device">> := <<"tx@1.0">>, <<"length">> := 8387 }},
        read_offset(Store, TXID, #{})
    ),
    % The offset is retained, so the routes are asked once per message.
    ?assertMatch({ok, #{ <<"length">> := 8387 }}, read_offset(Local, TXID, #{})),
    Node = hb_http_server:start_node(#{ <<"store">> => [Store] }),
    {ok, Msg} = hb_http:get(Node, <<"/", ItemID/binary>>, #{}),
    ?assert(hb_message:verify(Msg, all, #{})),
    ?assertEqual(ItemID, hb_message:id(Msg, signed, #{})),
    ?assertEqual(<<"1984">>, hb_ao:get(<<"data">>, Msg, #{})).
