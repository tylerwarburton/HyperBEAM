%%% @doc Implementation of Arweave's GraphQL API to gain access to specific 
%%% items of data stored on the network.
%%% 
%%% This module must be used to get full HyperBEAM `structured@1.0' form messages
%%% from data items stored on the network, as Arweave gateways do not presently
%%% expose all necessary fields to retrieve this information outside of the
%%% GraphQL API. When gateways integrate serving in `httpsig@1.0' form, this
%%% module will be deprecated.
-module(hb_client_gateway).
%% Raw access primitives:
-export([query/2, query/3, query/4, query/5]).
-export([read/2, data/2, result_to_message/2, item_spec/0]).
%% Application-specific data access functions:
-export([device/3, location/2]).
-include_lib("include/hb.hrl").
-include_lib("eunit/include/eunit.hrl").

%% @doc Get a data item (including data and tags) by its ID, using the node's
%% GraphQL peers.
%% It uses the following GraphQL schema:
%% type Transaction {
%%   id: ID!
%%   anchor: String!
%%   signature: String!
%%   recipient: String!
%%   owner: Owner { address: String! key: String! }!
%%   fee: Amount!
%%   quantity: Amount!
%%   data: MetaData!
%%   tags: [Tag { name: String! value: String! }!]!
%% }
%% type Amount {
%%   winston: String!
%%   ar: String!
%% }
read(ID, Opts) ->
    {Query, Variables} = case maps:is_key(<<"subindex">>, Opts) of
      true -> 
        Tags = subindex_to_tags(maps:get(<<"subindex">>, Opts)),
        {
            <<
                "query($transactionIds: [ID!]!) { ",
                    "transactions(ids: $transactionIds,",
                    "tags: ", (Tags)/binary , ",",
                    "first: 1){ ",
                        "edges { ", (item_spec())/binary , " } ",
                    "} ",
                "} "
            >>,
            #{
                <<"transactionIds">> => [hb_util:human_id(ID)]
            }
        };
      false -> 
        {
            <<
                "query($transactionIds: [ID!]!) { ",
                    "transactions(ids: $transactionIds, first: 1){ ",
                        "edges { ", (item_spec())/binary , " } ",
                    "} ",
                "} "
            >>,
            #{
                <<"transactionIds">> => [hb_util:human_id(ID)]
            }
        }
    end,
    case query(Query, Variables, Opts) of
        {error, Reason} -> {error, Reason};
        {ok, GqlMsg} ->
            case hb_ao:get(<<"data/transactions/edges/1/node">>, GqlMsg, Opts) of
                not_found ->
                    ?event({read_not_found, {id, ID}, {gql_msg, GqlMsg}}),
                    {error, not_found};
                Item ->
                    ?event({read_found, {id, ID}, {item, Item}}),
                    result_to_message(ID, Item, Opts)
            end
    end.

%% @doc Gives the fields needed to construct an Arweave message.
item_spec() ->
    <<"""
        node {
            id
            bundledIn { id }
            anchor
            signature
            recipient
            owner { key }
            fee { winston }
            quantity { winston }
            tags { name value }
            data { size }
        }
        cursor
    """>>.

%% @doc Get the data associated with a transaction by its ID, using the node's
%% Arweave `gateway' peers. The item is expected to be available in its 
%% unmodified (by caches or other proxies) form at the following location:
%%      https://<gateway>/raw/<id>
%% where `<id>' is the base64-url-encoded transaction ID.
data(ID, Opts) ->
    Req = #{
        <<"multirequest-accept-status">> => 200,
        <<"multirequest-responses">> => 1,
        <<"path">> => <<"/arweave/raw/", ID/binary>>,
        <<"method">> => <<"GET">>
    },
    case hb_http:request(Req, Opts) of
        {ok, Data} when is_binary(Data) -> {ok, Data};
        {ok, Res} ->
            Data =
                case hb_maps:find(<<"data">>, Res, Opts) of
                    {ok, D} -> D;
                    _ -> hb_ao:get(<<"body">>, Res, <<>>, Opts)
                end,
            ?event(gateway,
                {data,
                    {id, ID},
                    {response, Res},
                    {data, Data}
                }
            ),
            {ok, Data};
        Res ->
            ?event(gateway, {request_error, {id, ID}, {response, Res}}),
            {error, no_viable_gateway}
    end.

%% @doc Find the location of the scheduler based on its ID, through GraphQL.
%% Current AO location records use lowercase tags. Nodes may disable the
%% fallback to legacy capitalized tags with `scheduler-legacy-locations'.
location(Address, Opts) ->
    maybe
        % Fallback to legacy capitalized tags if a lower-case result is not
        % available and `scheduler-legacy-locations' is enabled.
        Error = {error, _} ?=
            do_location(Address, <<"type">>, <<"[\"location\"]">>, Opts),
        true ?= hb_opts:get(scheduler_legacy_locations, true, Opts)
            orelse Error,
        do_location(
            Address,
            <<"Type">>,
            <<"[\"Location\", \"Scheduler-Location\"]">>,
            Opts
        )
    end.
do_location(Address, TagName, TagValues, Opts) ->
    Query =
        <<"query($Addresses: [String!]!) { ",
                "transactions(",
                "owners: $Addresses, ",
                "tags: { name: \"", TagName/binary, "\" values: ",
                    TagValues/binary, " }, ",
                "first: 1",
            "){ ",
                "edges { ",
                    (item_spec())/binary ,
                " } ",
            "} ",
        "}">>,
    Variables = #{ <<"Addresses">> => [Address] },
    case query(Query, Variables, Opts) of
        {error, Reason} ->
            ?event({scheduler_location, {query, Query}, {error, Reason}}),
            {error, Reason};
        {ok, GqlMsg} ->
            ?event({scheduler_location_req, {query, Query}, {response, GqlMsg}}),
            case hb_ao:get(<<"data/transactions/edges/1/node">>, GqlMsg, Opts) of
                not_found ->
                    ?event(scheduler_location,
                        {graphql_scheduler_location_not_found,
                            {address, Address}
                        }
                    ),
                    {error, not_found};
                Item = #{ <<"id">> := ID } ->
                    ?event(scheduler_location,
                        {found_via_graphql,
                            {address, Address},
                            {id, ID}
                        }
                    ),
                    location_result(result_to_message(ID, Item, Opts), Opts)
            end
    end.

location_result({ok, Location}, Opts) ->
    case hb_ao:get_first(
        [
            {Location, <<"url">>},
            {Location, <<"location">>}
        ],
        not_found,
        Opts
    ) of
        not_found -> {error, not_found};
        _ -> {ok, Location}
    end;
location_result(Error, _Opts) ->
    Error.

%% @doc AO-Core devices are defined primarily by their specification IDs. To find
%% compatible device implementations we must query for messages with the
%% appropriate tags and signatures.
device(SpecID, TrustedSigners, Opts) ->
    Queries = device_queries(SpecID, TrustedSigners, Opts),
    case device_result(Queries, Opts) of
        {error, _} = Error ->
            Error;
        {ok, []} ->
            ?event(
                device_load,
                {no_viable_device_implementations, {device, SpecID}}
            ),
            {error, not_found};
        {ok, Items} ->
            ?event(
                device_load,
                {implementations_found_via_graphql,
                    {device, SpecID},
                    {implementations, length(Items)}
                }
            ),
            {
                ok,
                [
                    ID
                ||
                    #{ <<"node">> := #{ <<"id">> := ID } } <- Items
                ]
            }
    end.

device_result([], _Opts) ->
    {ok, []};
device_result([{Query, Variables} | Rest], Opts) ->
    case query(Query, Variables, Opts) of
        {error, Reason} ->
            ?event({device_read_failed, {query, Query}, {error, Reason}}),
            {error, Reason};
        {ok, GqlMsg} ->
            ?event({device_query_success, {query, Query}, {response, GqlMsg}}),
            case hb_ao:get(<<"data/transactions/edges">>, GqlMsg, Opts) of
                X when X =:= not_found orelse X =:= [] ->
                    device_result(Rest, Opts);
                Items ->
                    {ok, Items}
            end
    end.

device_queries(SpecID, TrustedSigners, Opts) ->
    SignerPolicies =
        [
            {Address, signer_valid_until_height(Signer, Opts)}
        ||
            Signer <- TrustedSigners,
            Address <- [trusted_signer_address(Signer, Opts)],
            Address =/= undefined
        ],
    case lists:any(fun({_Signer, Height}) -> Height =/= undefined end, SignerPolicies) of
        false ->
            Signers = [Signer || {Signer, _Height} <- SignerPolicies],
            [device_query(SpecID, Signers, undefined)];
        true ->
            [
                device_query(SpecID, [Signer], Height)
            ||
                {Signer, Height} <- SignerPolicies
            ]
    end.

trusted_signer_address(Signer, _Opts) when is_binary(Signer) ->
    Signer;
trusted_signer_address(Signer, Opts) when is_map(Signer) ->
    hb_maps:get(<<"address">>, Signer, undefined, Opts);
trusted_signer_address(_Signer, _Opts) ->
    undefined.

signer_valid_until_height(Signer, Opts) when is_map(Signer) ->
    case hb_maps:get(<<"valid-until-height">>, Signer, undefined, Opts) of
        undefined -> undefined;
        Height -> hb_util:int(Height)
    end;
signer_valid_until_height(_Signer, _Opts) ->
    undefined.

device_query(SpecID, TrustedSigners, ValidUntilHeight) ->
    BlockFilter =
        case ValidUntilHeight of
            undefined -> <<>>;
            _ -> <<"block: { max: $validUntilHeight }, ">>
        end,
    ValidUntilVar =
        case ValidUntilHeight of
            undefined -> <<>>;
            _ -> <<", $validUntilHeight: Int">>
        end,
    Query =
        <<"query($specid: [String!], $trusted: [String!]", ValidUntilVar/binary, ") { ",
                "transactions(",
                "owners: $trusted, ",
                "tags: { name: \"implements-device\" values: $specid }, ",
                BlockFilter/binary,
                "first: 1",
            "){ ",
                "edges { ",
                    (item_spec())/binary ,
                " } ",
            "} ",
        "}">>,
    Variables0 = #{ <<"trusted">> => TrustedSigners, <<"specid">> => [SpecID] },
    Variables =
        case ValidUntilHeight of
            undefined -> Variables0;
            _ -> Variables0#{ <<"validUntilHeight">> => ValidUntilHeight }
        end,
    {Query, Variables}.

%% @doc Run a GraphQL request encoded as a binary. The node message may contain 
%% a list of URLs to use, optionally as a tuple with an additional map of options
%% to use for the request.
query(Query, Opts) ->
    query(Query, undefined, Opts).
query(Query, Variables, Opts) ->
    query(Query, Variables, undefined, Opts).
query(Query, Variables, Node, Opts) ->
    query(Query, Variables, Node, undefined, Opts).
query(Query, Variables, Node, Operation, Opts) ->
    % Either use the given node if provided, or use the local machine's routes
    % to find the GraphQL endpoint.
    Path =
        case Node of
            undefined -> <<"/graphql">>;
            _ -> << Node/binary, "/graphql">>
        end,
    ?event(graphql,
        {request,
            {path, Path},
            {query, Query},
            {variables, Variables},
            {operation, Operation}
        }
    ),
    CombinedQuery =
        maps:filter(
            fun(_, V) -> V =/= undefined end,
            #{
                <<"query">> => Query,
                <<"variables">> => Variables,
                <<"operationName">> => Operation
            }
        ),
    % Find the routes for the GraphQL API.
    Res = hb_http:request(
        #{
            % Add options for the HTTP request, in case it is being made to
            % many nodes.
            <<"multirequest-responses">> => 1,
            <<"multirequest-admissible-status">> => 200,
            <<"multirequest-admissible">> =>
                #{
                    <<"device">> => <<"query@1.0">>,
                    <<"path">> => <<"has-results">>
                },
            % Main request fields
            <<"method">> => <<"POST">>,
            <<"path">> => <<"/graphql">>,
            <<"content-type">> => <<"application/json">>,
            <<"body">> => hb_json:encode(CombinedQuery)
        },
        Opts
    ),
    case Res of
        {ok, Msg} ->
            {ok, hb_json:decode(hb_ao:get(<<"body">>, Msg, <<>>, Opts))};
        {error, Reason} -> {error, Reason}
    end.

%% @doc Takes a GraphQL item node, matches it with the appropriate data from a
%% gateway, then returns `{ok, ParsedMsg}'.
result_to_message(Item, Opts) ->
    case hb_maps:get(<<"id">>, Item, not_found, Opts) of
        ExpectedID when is_binary(ExpectedID) ->
            result_to_message(ExpectedID, Item, Opts);
        _ ->
            result_to_message(undefined, Item, Opts)
    end.
%% @doc Load an L1 transaction in its native form.
result_to_message(ExpectedID, #{ <<"bundledIn">> := null }, Opts) ->
    hb_ao:resolve(
        #{ <<"device">> => <<"arweave@2.9">> },
        #{ <<"path">> => <<"tx">>, <<"tx">> => ExpectedID },
        Opts
    );
result_to_message(ExpectedID, Item, Opts) ->
    GQLOpts =
        Opts#{
            <<"hashpath">> => ignore,
            <<"cache-control">> => [<<"no-cache">>, <<"no-store">>]
        },
    % We have the headers, so we can get the data.
    Data =
        case hb_maps:get(<<"data">>, Item, not_found, GQLOpts) of
            #{ <<"size">> := Zero } when Zero =:= <<"0">> orelse Zero =:= 0 -> <<>>;
            BinData when is_binary(BinData) -> BinData;
            _ ->
                {ok, Bytes} = data(ExpectedID, Opts),
                Bytes
        end,
    DataSize = byte_size(Data),
    ?event(gateway, {data, {id, ExpectedID}, {data, Data}, {item, Item}}, Opts),
    % Convert the response to an ANS-104 message.
    Tags = hb_maps:get(<<"tags">>, Item, tags_not_found, GQLOpts),
	Signature =
        hb_util:decode(
            hb_maps:get(<<"signature">>, Item, not_found, GQLOpts)
        ),
	SignatureType =
        case byte_size(Signature) of
            64 -> {eddsa, ed25519};
            65 -> ethereum;
            512 -> {rsa, 65537};
            _ -> unsupported_tx_signature_type
        end,
    TX =
        ar_tx:reset_ids(#tx {
            format = ans104,
            anchor =
                normalize_null(hb_maps:get(<<"anchor">>, Item, not_found, GQLOpts)),
            signature = Signature,
            signature_type = SignatureType,
            target =
                decode_or_null(
                    hb_ao:get_first(
                        [
                            {Item, <<"recipient">>},
                            {Item, <<"target">>}
                        ],
                        GQLOpts
                    )
                ),
            owner =
                hb_util:decode(
                    hb_util:deep_get(<<"owner/key">>, Item, GQLOpts)
                ),
            tags =
                [
                    {normalize_graphql_tag_name(Name, Value), Value}
                ||
                    #{<<"name">> := Name, <<"value">> := Value} <- Tags
                ],
            data_size = DataSize,
            data = Data
        }),
    ?event({raw_ans104, TX}),
    ?event({ans104_form_response, TX}),
    TABM = hb_message:convert(TX, tabm, <<"ans104@1.0">>, Opts),
    ?event({decoded_tabm, TABM}),
    Structured = hb_message:convert(TABM, <<"structured@1.0">>, tabm, Opts),
    % Some graphql nodes do not grant the `anchor' or `last_tx' fields, so we
    % verify the data item and optionally add the explicit keys as committed
    % fields _if_ the node desires it.
    Embedded =
        case try ar_bundles:verify_item(TX) catch _:_ -> false end of
            true ->
                ?event({gql_verify_succeeded, Structured}),
                Structured;
            _ ->
                % The item does not verify on its own, but does the node choose
                % to trust the GraphQL API anyway?
                case hb_opts:get(ans104_trust_gql, false, Opts) of
                    false ->
                        ?event(
                            warning,
                            {gql_verify_failed, returning_unverifiable_tx}
                        ),
                        Structured;
                    true ->
                        % The node trusts the GraphQL API, so we add the explicit
                        % keys as committed fields.
                        ?event(warning,
                            {gql_verify_failed,
                                adding_trusted_fields,
                                {tags, Tags}
                            }
                        ),
                        Comms = hb_maps:get(<<"commitments">>, Structured, #{}, Opts),
                        AttName = hd(hb_maps:keys(Comms, Opts)),
                        Comm = hb_maps:get(AttName, Comms, not_found, Opts),
                        Structured#{
                            <<"commitments">> => #{
                                AttName =>
                                    Comm#{
                                        <<"trusted-keys">> =>
                                            hb_ao:normalize_keys(
                                                [
                                                    hb_ao:normalize_key(Name)
                                                ||
                                                    #{ <<"name">> := Name } <-
                                                        hb_maps:values(
                                                            hb_ao:normalize_keys(
                                                                Tags,
                                                                Opts
                                                            ),
                                                            Opts
                                                        )
                                                ],
												Opts
                                            )
                                    }
                            }
                        }
                end
        end,
    {ok, Embedded}.

normalize_null(null) -> <<>>;
normalize_null(not_found) -> <<>>;
normalize_null(Bin) when is_binary(Bin) -> Bin.

decode_or_null(Bin) when is_binary(Bin) ->
    hb_util:decode(Bin);
decode_or_null(_) ->
    <<>>.

%% @doc Some gateway GraphQL responses can expose TABM link tag names with
%% `+' decoded as a form-space, yielding `balances link' instead of the
%% canonical `balances+link'. Normalize only when the value has HyperBEAM's ID
%% shape, so ordinary tags whose names end in ` link' remain unchanged.
normalize_graphql_tag_name(Name, Value)
        when is_binary(Name), byte_size(Name) >= 5 ->
    Size = byte_size(Name),
    case {
        binary:part(Name, Size - 5, 5),
        ?IS_ID(Value)
    } of
        {<<" link">>, true} ->
            Prefix = binary:part(Name, 0, Size - 5),
            <<Prefix/binary, "+link">>;
        _ ->
            Name
    end;
normalize_graphql_tag_name(Name, _Value) ->
    Name.

%% @doc Takes a list of messages with `name' and `value' fields, and formats
%% them as a GraphQL `tags' argument.
subindex_to_tags(Subindex) ->
    Formatted =
        lists:map(
            fun(Spec) ->
                io_lib:format(
                    "{ name: \"~s\", values: [\"~s\"]}",
                    [
                        hb_ao:get(<<"name">>, Spec),
                        hb_ao:get(<<"value">>, Spec)
                    ]
                )
            end,
            hb_util:message_to_ordered_list(Subindex)
        ),
    ListInner =
        hb_util:bin(
            string:join([lists:flatten(E) || E <- Formatted], ", ")
        ),
    <<"[", ListInner/binary, "]">>.

%%% Tests
device_valid_until_height_query_test() ->
    SpecID = <<"spec">>,
    Alice = <<"alice">>,
    Bob = <<"bob">>,
    [{BaseQuery, BaseVars}] = device_queries(SpecID, [Alice, Bob], #{}),
    ?assertEqual([Alice, Bob], maps:get(<<"trusted">>, BaseVars)),
    ?assertEqual(nomatch, binary:match(BaseQuery, <<"validUntilHeight">>)),
    [{AliceQuery, AliceVars}, {BobQuery, BobVars}] =
        device_queries(
            SpecID,
            [#{ <<"address">> => Alice, <<"valid-until-height">> => 1543210 }, Bob],
            #{}
        ),
    ?assertEqual([Alice], maps:get(<<"trusted">>, AliceVars)),
    ?assertEqual(1543210, maps:get(<<"validUntilHeight">>, AliceVars)),
    ?assert(
        binary:match(AliceQuery, <<"block: { max: $validUntilHeight }">>)
            =/= nomatch
    ),
    ?assertEqual([Bob], maps:get(<<"trusted">>, BobVars)),
    ?assertEqual(nomatch, binary:match(BobQuery, <<"validUntilHeight">>)).

graphql_link_tag_name_normalization_test() ->
    ID = <<"5wE86A8Z3QRqYIQBDQY-rws-Lb-b2uaXO7yfdf4_NAg">>,
    ?assertEqual(
        <<"balances+link">>,
        normalize_graphql_tag_name(<<"balances link">>, ID)
    ),
    ?assertEqual(
        <<"26wkmnkmn3u99knn5dukjd0cgyotdiw6jj6eyc_ied4+link">>,
        normalize_graphql_tag_name(
            <<"26wkmnkmn3u99knn5dukjd0cgyotdiw6jj6eyc_ied4 link">>,
            ID
        )
    ),
    ?assertEqual(
        <<"balances link">>,
        normalize_graphql_tag_name(<<"balances link">>, <<"not-an-id">>)
    ),
    ?assertEqual(
        <<"plain link tag">>,
        normalize_graphql_tag_name(<<"plain link tag">>, ID)
    ),
    ?assertEqual(
        <<"already+link">>,
        normalize_graphql_tag_name(<<"already+link">>, ID)
    ).

ans104_no_data_item_test() ->
    % Start a random node so that all of the services come up.
    _Node = hb_http_server:start_node(#{}),
    {ok, Res} = read(<<"BOogk_XAI3bvNWnxNxwxmvOfglZt17o4MOVAdPNZ_ew">>, #{}),
    ?event(gateway, {get_ans104_test, Res}),
    ?event(gateway, {signer, hb_message:signers(Res, #{})}),
    ?assert(true).

%% @doc A location result must name an HTTP endpoint.
location_requires_endpoint_test() ->
    URL = #{ <<"url">> => <<"https://example.com">> },
    Location = #{ <<"location">> => <<"https://example.com">> },
    ?assertEqual({ok, URL}, location_result({ok, URL}, #{})),
    ?assertEqual({ok, Location}, location_result({ok, Location}, #{})),
    ?assertEqual(
        {error, not_found},
        location_result({ok, #{ <<"superseded-by">> => <<"id">> }}, #{})
    ).

%% @doc Test that we can get the scheduler location.
scheduler_location_test() ->
    % Start a random node so that all of the services come up.
    _Node = hb_http_server:start_node(#{}),
    {ok, Res} =
        location(
            <<"fcoN_xJeisVsPXA-trzVAuIiqO3ydLQxM-L4XbrQKzY">>,
            #{}
        ),
    ?event(gateway, {get_scheduler_location_test, Res}),
    ?assertEqual(<<"Scheduler-Location">>, hb_ao:get(<<"Type">>, Res, #{})),
    ?event(gateway, {scheduler_location, {explicit, hb_ao:get(<<"url">>, Res, #{})}}),
    % Will need updating when Legacynet terminates.
    ?assertEqual(<<"https://su-router.ao-testnet.xyz">>, hb_ao:get(<<"url">>, Res, #{})).

%% @doc Test l1 message from graphql
l1_transaction_test() ->
    ID = <<"uJBApOt4ma3pTfY6Z4xmknz5vAasup4KcGX7FJ0Of8w">>,
    Node = hb_http_server:start_node(
        #{ <<"store">> => [#{ <<"store-module">> => hb_store_gateway }] }
    ),
    ClientOpts = #{ <<"store">> => [hb_test_utils:test_store(hb_store_volatile)] },
    {ok, Res} = hb_http:get(Node, ID, ClientOpts),
    ?event(gateway, {l1_transaction, Res}),
    Devices = hb_message:commitment_devices(Res, ClientOpts),
    ?assert(lists:member(<<"tx@1.0">>, Devices)),
    ?assertNot(lists:member(<<"ans104@1.0">>, Devices)),
    ?assert(hb_message:verify(Res, all, ClientOpts)),
    Data = maps:get(<<"data">>, Res),
    ?assertEqual(<<"Hello World">>, Data).

%% @doc Test l2 message from graphql
l2_dataitem_test() ->
    _Node = hb_http_server:start_node(#{}),
    {ok, Res} = read(ID = <<"oyo3_hCczcU7uYhfByFZ3h0ELfeMMzNacT-KpRoJK6g">>, #{}),
    ?event(gateway, {l2_dataitem, Res}),
    Opts = #{},
    CommitmentType = hb_util:deep_get(
        [<<"commitments">>, ID, <<"type">>],
        Res,
        not_found,
        Opts
    ),
    ?assertEqual(?RSA_SIGN_TYPE, CommitmentType),
    Data = maps:get(<<"data">>, Res),
    ?assertEqual(<<"Hello World">>, Data).

%% @doc ed25519 L2 Transaction test
l2_dataitem_ed25519_test() ->
    _Node = hb_http_server:start_node(#{}),
    ID = <<"AwrAs-HaBlc8xeI8sw6Wpbi7A0weQWeXYwW20CpX5oM">>,
    {ok, Res} = read(ID, #{}),
    ?event(gateway, {l2_dataitem, Res}),
    Opts = #{},
    CommitmentType = hb_util:deep_get(
        [<<"commitments">>, ID, <<"type">>],
        Res,
        not_found,
        Opts
    ),
    ?assertEqual(?EDDSA_SIGN_TYPE, CommitmentType),
    CommitmentCommitter = hb_util:deep_get(
        [<<"commitments">>, ID, <<"committer">>],
        Res,
        not_found,
        Opts
    ),
    ?assertEqual(<<"ejhYD9Cw9VCsVik6yGLoclo3CLRvAITHTZamLY_6ro4">>, CommitmentCommitter),
    %% Check Data
    Data = maps:get(<<"data">>, Res),
    ?assertEqual(<<"{\"displayName\":\"Test Hub\",\"description\":\"This is a test hub created in the test suite\",\"externalurl\":\"\",\"image\":\"\"}">>, Data).

%% @doc Test optimistic index
ao_dataitem_test() ->
    _Node = hb_http_server:start_node(#{}),
    {ok, Res} = read(<<"oyo3_hCczcU7uYhfByFZ3h0ELfeMMzNacT-KpRoJK6g">>, #{}),
    ?event(gateway, {l2_dataitem, Res}),
    Data = maps:get(<<"data">>, Res),
    ?assertEqual(<<"Hello World">>, Data).
